external updating procedure DoBuyOutAgreement(record CredManVc,Date,Boolean,var record IVVc);
external inner procedure CredManUpdate(var record CredManVc,Integer,string);
external inner procedure CredManSumup(var record CredManVc);
external inner procedure CredManVc_PasteCurncyCode(var record CredManVc);
external procedure BuildCredManPayPlanCust(record CredManVc,var record CredManPayPlanVc,record CredManSetBlock,Boolean,Boolean);
external procedure CheckBuyOutAgreement(record CredManVc,var record RcVc);
external inner function Boolean PasteCustInCredMan(var record CredManVc);
external inner procedure CredManVc_PasteType(var record CredManVc);
external inner function Integer CreateCreditNoteIV(record IVVc,Integer,var record IVVc,string,Boolean);
//external updating procedure PrintMultiDocDocument(string,string,string,Integer,Boolean,string);
//external updating procedure PrintMultiDocDocument2(string,string,string,Integer,Boolean,string,record RcVc);
external inner function string 60 AddObjectToObjectList(string,string);
external inner procedure VIVc_PasteInvDate(var record VIVc);
external inner procedure VIDDefault(Integer,string,var record VIVc);
external inner function Boolean VIVc_PasteAccNumber(var record VIVc,string,Boolean,Integer);
external inner function Boolean VIVc_PasteVECode(var record VIVc,Integer,Boolean,Boolean,var string);
external inner procedure VIVc_PastePayDeal(var record VIVc);
external inner procedure VICalcVals(var record VIVc);
external inner procedure VISumup(record VIVc,var val);
external updating procedure CreateSingleAgreementInvoice2(record CredManVc,Date,var record IVVc,string,Boolean,Boolean);
external inner procedure IVVc_PasteInvDate(var record IVVc,record LocalMachineBlock,var Integer,var Integer);
external updating procedure ReceiptEntryCreateReceipts(record SMVc,Boolean,var val);
external updating procedure CreateCredHist(Longint,Integer,string,val,Integer,string,Date,Longint,Date,var Longint,Date,Date,Integer,Longint,val,Longint,val);
external updating procedure UpdateCredHistBalancePartialBuyout(Longint,Longint);
external inner procedure PrintEBSDocument(string,string,record RcVc,string,string,var area);
external inner procedure OPPastePayMode(var record OPVc);
external inner function Boolean PasteInvIn2OPr(var record OPVc,Integer,Date,Integer,val,var string,Boolean,var Boolean);
external inner function Boolean PasteInvIn2IPr(var record IPVc,Integer,Date,var val,Boolean,var Boolean);
external inner procedure IPPastePayMode(var record IPVc);
external inner procedure OPVc_PasteRecVal(var record OPVc,Integer);
external updating procedure CheckAndCreateAutoMailFromInvoice(record IVVc);
external procedure GenerateAgreementSchedule(record CredManVc,Boolean,Boolean);
external inner function Boolean ExternalDocumentEnabled(string);
external updating procedure PrintCredManDocument(string,string,record CredManVc,Integer,Boolean,string);
external inner procedure GetCurUser(var record UserVc);
external updating procedure CreateProlongInvoice(record CredManVc,var record IVVc);
external inner function string 50 NextLegalSerNr(string,LongInt,Date,string,string,string);
external function Boolean GetCredLegalNrLine(string,string,Date,string,string,var row CredLegalInvNrBlock,Integer);
external procedure FindListCredLegalSerNr(string,string,string,string,string,Integer,var array string);
external procedure BuildCredManInfoMatrix(record CredManVc,record CredManPayPlanVc,record CredManSetBlock,var record SMVc);
external updating procedure RecreateAgreementDocument(var record AgreementDocVc,Boolean);
external function val GetRateAmount(record CredManVc,val,Integer,Date,Date,val,Boolean,Boolean);
external function Boolean FindCEOContact(string,var record CUVc);
external updating procedure GetCustomerLoanAmounts2(var record IPVc,var integer,string,var string,var val,string,boolean,string,string,var vector Boolean,Date,Longint);
external function string 255 CM_GetCredManTypePurchItemAccount2(record CredManVc,string,Boolean);
external updating procedure CreatePauseRecord(Longint,Date,val,val,val,val,Integer,string);
external function Integer GetProlongMonths(record CredManVc);
external function Date GetLastInvoiceDate(record CredManVc,Date);
external procedure GetCredManOverdueInvoices(record CredManVc,record CredManSetBlock,Date,Date,var Integer,var val,var array string,Boolean,Boolean,var Date);
external procedure SetupLoanInvoice(record CredManVc,record CredManSetBlock,var record IVVc,Date,Date,Date,Boolean,Boolean,string,Integer,Boolean);
external procedure SetupLoanInvoice2(record CredManVc,record CredManSetBlock,var record IVVc,Date,Date,Date,Boolean,Boolean,string,Integer,Boolean,string);
external procedure AddLateFees(record CredManVc,record CredManSetBlock,var record IVVc,Integer,val,array string);
external procedure SetupOverdueLangCode(var record IVVc,record CredManVc);
external updating procedure StoreLoanInvoice(var record IVVc,record CredManVc);
external procedure CM_AddPrepayments(var record IVVc,record CredManVc,record CMInvoicingBlock);
external procedure SetupInvoiceRowAndAdd(record CredManVc,var record IVVc,string,string,val,val,val,Integer,Longint);
remote procedure RecreateAgreementSchedule3CallBack(record CredManVc,Integer);
external function Boolean HasSuretyPerson();
external procedure DistributeCredHistValues(var record IPVc,Integer);
external procedure AddInstalmentFixed(record CredManVc,record CredManPayPlanVc,record CredManSetBlock,val,Date,var val,var val,Date,Date,val);
external procedure GetBuyOutData(record CredManVc,record CredManPayPlanVc,record CredManSetBlock,var Date,var val,var val);
external inner procedure IVVc_PastePrice(var record IVVc,record IVVc,Integer,var string);
external procedure MergeItemsInInvoice(var record IVVc,record CredManVc);
external function Boolean AgreementPaidOut2(record CredManVc,var val);
external function Integer GetInterestPaymentType(record CredManVc);
external function Boolean UseFullBuyoutProcedure();
external procedure AddBuyOutFee(record CredManVc,var record IVVc,val);
external procedure CheckAndAddHiddenPrinciple(var record IVVc,record CMInvoicingBlock);
remote procedure RefinCredManPartialWClassSubmitCallBack(string,Integer,Integer,Boolean);
forward updating procedure CreateCredmanPurchaseInvoice(record CredManVc,val,var record VIVc,val,Boolean,Date);
external inner procedure FindUserMailboxName(string,var string,var string);
external inner function Boolean CheckAttachedFilesSizeToLetter(record LetVc);
external procedure CheckBuyOutAgreementAsync(record CredManVc,Date,Integer);
remote procedure RefinCredManPartial2WClassSubmitCallBack(Integer,record RcVc,Boolean);
external inner function Boolean PasteInvIn2IPr(var record IPVc,Integer,Date,var val,Boolean,var Boolean);
external inner procedure IPVc_PasteRecCurncy(var record IPVc,Integer);
external inner procedure IPVc_PasteRecVal(var record IPVc,Integer);
external inner procedure IPSumup(var record IPVc);
external inner function Boolean IPVc_PasteCustCode(var record IPVc,Integer,var LongInt);
external function string 255 GetReceiptsPayMode(record IVVc);
remote procedure CredManCreditBeforePauseCallBack(Integer,Integer,record RcVc);
external function Boolean FindCredManAcc(record CredManVc,var record CredManAccVc);
external procedure ExternalPrintCredManScheduleForm(record CredManVc,var vector string);
external updating function Boolean StoreEmailRecord(record LTxtVc,var record MailVc,record RcVc);
external inner function string 255 CredManCustEmail(record CUVc);
external inner function string 255 StrReplace(string,string,string);
external function boolean getAdditionalLangCode(record CredManVc,string,var string);
external function string 20 getAdditionalCredManLangCode(record CredManVc);

global
updating function Boolean ArchiveExistingDocument(record AgreementDocVc ADr,var string oldname)
begin
  record RLinkVc RLr,RL2r;
  record Attach2Vc Attachr;
  record ArchiveVc Archiver;
  Boolean res;
  
  if (ReadRecordLink(ADr,1,Attachr,RLr)) then begin
    if (ReadRecordLink(ADr,1,Archiver,RL2r)==false) then begin
      RecordNew(Archiver);
      Archiver.SerNr = NextSerNr("ArchiveVc",CurrentDate,-1,false,"");
      Archiver.FolderName = "OLD";
      if (RecordStore(Archiver,true)) then begin
        CreateRecordLink(ADr,CurrentCompany,Archiver,CurrentCompany);
      end;
    end;
    res = true;
    CreateRecordLink(Archiver,CurrentCompany,Attachr,CurrentCompany);
    oldname = Attachr.FileName;
    RecordRemove(RLr);
  end;

  ArchiveExistingDocument = res;
  return;
end;

global
procedure PasteCustInAgreementRemote(var record CredManVc CredManr,var string warning)
begin
  record BankVc Bankr;
  record CUVc CUr;
  record BaseCurBlock BCb;


  CUr.Code = CredManr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    CredManr.BankCode = CUr.AccOperator;
    Bankr.Code = CUr.AccOperator;
    if (ReadFirstMain(Bankr,1,true)) then begin
      CredManr.BankName = Bankr.Name;
    end;
    CredManr.BankAccount = CUr.BankAccount;
    if (nonblank(CUr.CurncyCode)) then begin
      CredManr.CurncyCode = CUr.CurncyCode;
    end else begin
      BlockLoad(BCb);
      CredManr.CurncyCode = BCb.BaseCur1;
    end;
    CredManVc_PasteCurncyCode(CredManr);
    warning = CUr.WarnText1;

  end;
  
  return;
end;


global
procedure CredManSetDatesCust(var record CredManVc CredManr,Boolean allf)
begin
  record CredManSetBlock CredManSetb;
  Date startd,finv,fint,tsd;
  Integer invday,prolongmonths;
  
  invday = CredManr.MonthlyPaymentDay;
  if (invday<1) then begin
    BlockLoad(CredManSetb);
    invday = CredManSetb.InvDay;
  end;
  startd = CredManr.startDate;
  //finv = AddMonth(startd,1); //CUST-CHANGED
  if (allf) then begin
    finv = AddDay(startd,25);//29
    tsd = finv;
    finv = AddDay(finv,invday - GetDay(finv));//-14
    if (tsd>finv) then begin
      finv = AddMonth(finv,1);
    end;
    CredManr.FirstInvDate = finv;
  end;
  CredManr.FirstIntDate = CredManr.startDate;
  //CredManr.MoneyTransferDate = CredManr.startDate;
  CredManUpdate(CredManr,-1,"");
  CredManSumup(CredManr);
  CredManr.endDate = AddMonth(CredManr.FirstInvDate,CredManr.InstalmentMonths - 1);
  prolongmonths = GetProlongMonths(CredManr);
  if (prolongmonths>0) then begin
    CredManr.endDate = AddMonth(CredManr.endDate,prolongmonths);
  end;


  return;
end;

function Boolean HasBOESureties(record CredManVc CredManr,Integer tnum)
begin
  string 255 code;
  record BOESuretyVc BSr;
  Boolean res;

  code = GetFieldValueByName(CredManr,"BOE" & tnum & "Code",-1);
  if (nonblank(code)) begin
    ResetLoop(BSr);
    BSr.CredManNr = CredManr.SerNr;
    BSr.BOENum = tnum;
    if (ReadFirstMain(BSr,2,true)) then begin
      if (nonblank(BSr.SuretyProvider1)) then begin
        res = true;
        goto LHasBOESureties;
      end;
    end;
  end;

LHasBOESureties:;
  HasBOESureties = res;
  return;
end;

function string 255 GetDocumentLangCode(record CredManVc CredManr,Integer type,Integer param)
begin
  string 255 res;
  record CredManTypeVc CMTr;
  record CUVc CUr;
  vector string 255 vVals;
  string 20 reslang;
  
  
  CMTr.Code = CredManr.Type;
  if (ReadFirstMain(CMTr,1,true)) then begin
    switch (type) begin
      case 1: res = CMTr.CredManLangCode;
      case 2: res = CMTr.ScheduleLangCode;
              if(nonblank(CMTr.ScheduleNoFeesLangCode))then begin
                ExternalPrintCredManScheduleForm(CredManr,vVals);
                if(stringtoval(vVals["F_TOTFEES"],m4val)==0)then begin
                  res = CMTr.ScheduleNoFeesLangCode;
                end;
              end;
      case 3: 
        if (blank(GetFieldValueByName(CredManr,"Surety" & param & "Signer",-1))) then begin
          res = CMTr.SuretyLangCode;
        end else begin
          res = CMTr.SuretyLangCode2;
        end;
        if (HasSuretyPerson) then begin
          CUr.Code = GetFieldValueByName(CredManr,"SuretyProvider" & param,-1);
          if (ReadFirstmain(CUr,1,true))then begin
            if (CUr.CustType==1) then begin
              if (blank(GetFieldValueByName(CredManr,"Surety" & param & "Signer",-1))) then begin
                res = CMTr.SuretyPersonLangCode;
              end else begin
                res = CMTr.SuretyPersonLangCode2;
              end;
            end;
          end;
        end;
      case 4: 
        if (CMTr.VekselisShortMonths>0 and CredManr.InstalmentMonths<CMTr.VekselisShortMonths) then begin
          if (HasBOESureties(CredManr,param)) then begin
            res = CMTr.VekselisShortLangCode;
          end else begin
            res = CMTr.VekselisShortLangCode2;
          end;
        end else begin
          if (HasBOESureties(CredManr,param)) then begin
            res = CMTr.VekselisLangCode;
          end else begin
            res = CMTr.VekselisLangCode2;            
          end;
        end;
    end;
  end;

  if (blank(res)) then begin
    res = getAdditionalCredManLangCode(CredManr);
  end;

  GetDocumentLangCode = res;
  return;
end;

function Boolean CredManIsPausedOnDate(record CredManVc CredManr,Date td)
begin
  Date sd,ed;
  record CredManChangeVc Changer;
  Boolean TrHs;
  Boolean res;
  
  Changer.CredManNr = CredManr.SerNr;
  Changer.TransDate = CredManr.FirstInvDate;
  TrHs = true;
  while (LoopKey("CredManDate",Changer,2,TrHs)) begin
    if (CredManr.SerNr!=Changer.CredManNr or Changer.TransDate>td) then begin
      TrHs = false;
    end else begin
      if (Changer.OKFlag==1) then begin
        if (Changer.PauseMonths>0) then begin
          ed = AddDay(AddMonth(Changer.TransDate,Changer.PauseMonths),-1);
          if (DateInRange(td,Changer.TransDate,ed)) then begin
            res = true;
            TrHs = false;
          end;
        end;
      end;
    end;
  end;

  CredManIsPausedOnDate = res;
  return;
end;

function Boolean CanPauseAgreement(record CredManVc CredManr,Date td,val months,var Integer err)
begin
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  Integer i,rwcnt;
  record CredManSetBlock CMb;
  Date td3,lastinv;
  Boolean res;

  res = true;
  err = 0;
  BlockLoad(CMb);
  BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CMb,true,false);
  rwcnt = MatRowCnt(CredManPayPlanr);
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(CredManPayPlanr,i,CredManPayPlanrw);
    if (CredManPayPlanrw.PlanType==5 or CredManPayPlanrw.PlanType==6 or CredManPayPlanrw.PlanType==7) then begin
      if ((nonblank(CredManPayPlanrw.ToDate) and td<=CredManPayPlanrw.ToDate) or td<=CredManPayPlanrw.TransDate) then begin
        res = false;
        err = 1;
      end else begin
        /*
        if (lastinv<CredManPayPlanrw.ToDate) then begin
          lastinv = CredManPayPlanrw.ToDate;
        end;
        */
      end;
      i = 0;
    end;
  end;

  if (CredManIsPausedOnDate(CredManr,td)) then begin
    res = false;
    err = 2;
  end;

  if (DateInRange(td,CredManr.startDate,CredManr.endDate)==false) then begin
    res = false;
    err = 3;
  end;

  if (CredManr.PrincipleDelayMonths>0 and months>0) then begin
    if (AddDay(AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths-1),-1)>td) then begin
      res = false;
      err = 4;
    end;
  end;

  CanPauseAgreement = res;
  return;
end;

global
updating procedure RecreateAgreementSchedule(record CredManVc CredManr)
begin
  record AgreementDocVc ADr,oldADr;
  row AgreementDocVc ADrw;
  record RcVc RepSpec;
  string 255 fn,langcode,oldname;
  Integer i,rwcnt;
  area filearea;

  ADr.RecordType = 0;
  ADr.RecordNr = CredManr.SerNr;
  ADr.Type = 2;
  if (ReadFirstKey("RecordNr",ADr,3,true)) then begin
    ArchiveExistingDocument(ADr,oldname);
    RecordCopy(oldADr,ADr);
    ADr.SignedByUs = 0;
    ADr.SignedByUsDate = "";
    ADr.SignedByUsTime = "";
    rwcnt = MatRowCnt(ADr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(ADr,i,ADrw);
      ADrw.Status = 0;
      ADrw.SignatureDate = "";
      ADrw.SignatureTime = "";
      MatRowPut(ADr,i,ADrw);
    end;
    if (RecordUpdate(oldADr,ADr,true)==0) then begin
      //should move this out to a function
      fn = "Schedule" & CredManr.SerNr & ".pdf";
      GenerateAgreementSchedule(CredManr,true,true);
      langcode = GetDocumentLangCode(CredManr,2,-1);
      if (ExternalDocumentEnabled("CredManScheduleForm")) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
        PrintEBSDocument("CredManScheduleForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
      end else begin
        PrintCredManDocument("CredManScheduleForm",fn,CredManr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        //PrintMultiDocDocument("CredManScheduleForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
      RecordLinkFile(fn,0,ADr,CurrentCompany);
      Delete_File(fn);
    end;
  end;

  return;
end;

global
procedure RecreateAgreementSchedule4(record CredManVc CredManr,Boolean fullf,Boolean resignf)
begin
  record AgreementDocVc ADr;
  transaction Boolean gSkipSchedRecalc;

  GenerateAgreementSchedule(CredManr,true,true);
  if (fullf) then begin
    ADr.RecordType = 0;
    ADr.RecordNr = CredManr.SerNr;
    ADr.Type = 2;
    if (ReadFirstKey("RecordNr",ADr,3,true)) then begin
      qupdating.RecreateAgreementDocument(ADr,resignf);
    end;
  end;

  return;
end;

global
updating procedure RecreateAgreementSchedule2(record CredManVc CredManr,Boolean fullf)
begin
  RecreateAgreementSchedule4(CredManr,fullf,true);
  return;
end;

global
updating procedure RecreateAgreementSchedule3(record CredManVc CredManr,Boolean fullf,Integer wn)
begin

  RecreateAgreementSchedule2(CredManr,fullf);
  ReadFirstMain(CredManr,1,true);
  ClientRemoteAsync.RecreateAgreementSchedule3CallBack(CredManr,wn);

  return;
end;

updating procedure CreateProlongSession(record CredManVc CredManr,Date td,val months,val prolongmonths,Integer prolongf,Integer recalcshedf,val rate,val addval)
begin
  record ProlongSessionVc PSr;

  RecordNew(PSr);
  PSr.SerNr = NextSerNr("ProlongSessionVc",CurrentDate,-1,false,"");
  PSr.TransDate = CurrentDate;
  PSr.TransTime = CurrentTime;
  PSr.SalesMan = CurrentUser;
  PSr.PauseDate = td;
  PSr.ProlongDate = td;
  PSr.PauseMonths = months;
  PSr.IntRate = rate;
  PSr.AddVal = addval;
  PSr.ProlongMonths = prolongmonths;
  PSr.ProlongWhenPaid = prolongf;
  PSr.RecalcSchedule = recalcshedf;
  PSr.CredManNr = CredManr.SerNr;
  RecordInsert(PSr,true);

  return;
end;

global
updating procedure DoPauseCredMan(var record CredManVc CredManr,val months,val prolongmonths,Date td,var Boolean res,var Boolean setf,record RcVc RepSpec,var record IVVc IVr,string purpose,var Integer err)
begin
  record AgreementDocVc ADr;
  record VIVc VIr;
  val tmonths,tprolongmonths,rate,addval;
  res = false;

//  if (nonblank(td)) then begin
    //RecordCopy(oldCredManr,CredManr);
    tprolongmonths = prolongmonths;
    tmonths = months;
    if (tprolongmonths==0) then begin
      tprolongmonths = blankval;
    end;
    if (nonblank(RepSpec.vals3)) then begin
      rate = RepSpec.vals3;
    end;
    if (nonblank(RepSpec.vals4)) then begin
      addval = RepSpec.vals4;
    end;
    if (tmonths==0) then begin
      tmonths = blankval;
    end;
    if (CanPauseAgreement(CredManr,td,months,err)) then begin
      if (RepSpec.flags[2]==1) then begin
        CreateProlongSession(CredManr,td,months,prolongmonths,RepSpec.flags[3],RepSpec.flags[1],RepSpec.vals3,RepSpec.vals4);
        CreateProlongInvoice(CredManr,IVr);
      end;
      if (RepSpec.flags[3]==0) then begin
        if (tmonths>0 or tprolongmonths>0 or rate>0 or addval>0) then begin
          CreatePauseRecord(CredManr.SerNr,td,tmonths,tprolongmonths,rate,addval,1,purpose);
        end;
        CredManSetDatesCust(CredManr,false);
        if (addval>0) then begin
          CreateCredmanPurchaseInvoice(CredManr,0,VIr,addval,true,td);
          GenerateAgreementSchedule(CredManr,true,true);
        end;
        setf = true;
      end else begin
        if (tmonths>0 or tprolongmonths>0 or rate>0 or addval>0) then begin
          CreatePauseRecord(CredManr.SerNr,td,tmonths,tprolongmonths,rate,addval,0,purpose);
        end;
      end;
      res = true;      
    end;
    //RecordUpdate(oldCredManr,CredManr,true);
//  end;

  return;
end;

global
procedure GetPauseCredManNextDate(record CredManVc CredManr,var Date td)
begin
  record CredManVc oldCredManr;
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  Integer i,rwcnt;
  record CredManSetBlock CMb;
  
  BlockLoad(CMb);
  BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CMb,true,false);
  rwcnt = MatRowCnt(CredManPayPlanr);
  td = CredManr.FirstInvDate;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(CredManPayPlanr,i,CredManPayPlanrw);
    if (CredManPayPlanrw.PlanType<5) then begin
      i = rwcnt;
      td = CredManPayPlanrw.TransDate;
    end;
  end;

  if (td<CredManr.FirstInvDate) then begin
    td = CredManr.FirstInvDate;
  end;

  if (CredManr.PrincipleDelayMonths>0) then begin
    if (AddDay(AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths),-1)>td) then begin
      td = AddMonth(CredManr.FirstInvDate,CredManr.PrincipleDelayMonths);
    end;
  end;
  
  return;
end;

global
procedure GetRefinCredManData(Longint CredManNr,Integer type,var record RcVc RepSpec,Boolean initf)
begin
  record CredManVc CredManr;
  record CredManPayPlanVc CredManPayPlanr;
  record CredManTypeVc CredManTyper;
  record RcVc planRepSpec;

  CredManr.SerNr = CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin

    if (blank(RepSpec.d1)) then begin
      RepSpec.d1 = CurrentDate;
    end; 

    planRepSpec.long1 = CredManr.SerNr;
    planRepSpec.d1 = RepSpec.d1;
    CheckBuyOutAgreement(CredManr,planRepSpec);
    RepSpec.vals0 = planRepSpec.vals0;
    RepSpec.vals1 = planRepSpec.vals1;
    //RepSpec.vals2 = 0;//should stay blank;
    RepSpec.vals3 = RepSpec.vals0 + RepSpec.vals1 + RepSpec.vals2;

    if (initf) then begin
      RepSpec.long1 = CredManr.SerNr;
      RepSpec.f1 = CredManr.Type;
      CredManTyper.Code = CredManr.Type;
      if (ReadFirstMain(CredManTyper,1,true)) then begin
        RepSpec.flags[0] = CredManTyper.InstalmentMonths;
      end;
      if (type==2) then begin
        RepSpec.f2 = CredManr.SuretyProvider1;
      end else begin
        RepSpec.f2 = CredManr.CustCode;
      end;

      RepSpec.flags[1] = CredManr.MonthlyPaymentDay;
      RepSpec.vals4 = 0;
      if (type==2) then begin
        RepSpec.vals4 = CredManr.SuretyInterestRate;
      end;

      RepSpec.ArtMode = type;
    end;
  end;

  return;
end;

procedure CopySingleCredManSurety(var record CredManVc CredManr,record CredManVc tCredManr,Integer num)
begin
  
    SetFieldValueByName(CredManr,"SuretyProvider" & num,GetFieldValueByName(tCredManr,"SuretyProvider" & num,-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Name",GetFieldValueByName(tCredManr,"Surety" & num & "Name",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Position",GetFieldValueByName(tCredManr,"Surety" & num & "Position",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Phone",GetFieldValueByName(tCredManr,"Surety" & num & "Phone",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Mobile",GetFieldValueByName(tCredManr,"Surety" & num & "Mobile",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "eMail",GetFieldValueByName(tCredManr,"Surety" & num & "eMail",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "ID",GetFieldValueByName(tCredManr,"Surety" & num & "ID",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr0",GetFieldValueByName(tCredManr,"Surety" & num & "Addr0",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr1",GetFieldValueByName(tCredManr,"Surety" & num & "Addr1",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr2",GetFieldValueByName(tCredManr,"Surety" & num & "Addr2",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr3",GetFieldValueByName(tCredManr,"Surety" & num & "Addr3",-1),-1);
    SetFieldValueByName(CredManr,"Surety" & num & "Addr4",GetFieldValueByName(tCredManr,"Surety" & num & "Addr4",-1),-1);

  return;
end;

procedure CopyCredManSurety(var record CredManVc CredManr,record CredManVc tCredManr)
begin
  
  CopySingleCredManSurety(CredManr,tCredManr,1);
  CopySingleCredManSurety(CredManr,tCredManr,2);
  CopySingleCredManSurety(CredManr,tCredManr,3);

  return;
end;

updating procedure CreateRefinCredMan(record RcVc RepSpec,var record CredManVc CredManr,record CredManVc tCredManr)
begin
  
  RecordNew(CredManr);
  CredManr.startDate = RepSpec.d1;
  CredManr.FirstInvDate = RepSpec.d1;
  CredManr.InvSum4 = RepSpec.vals3;
  CredManr.Type = RepSpec.f1;
  CredManr.CustCode = RepSpec.f2;
  PasteCustInCredMan(CredManr);
  CredManVc_PasteType(CredManr);
  CredManr.IntRate = RepSpec.vals4;
  CredManr.ClientContact = tCredManr.ClientContact;
  CredManr.MonthlyPaymentDay = RepSpec.flags[1];
  CredManr.InstalmentMonths = RepSpec.flags[0];
  CredManSetDatesCust(CredManr,true); 
  CopyCredManSurety(CredManr,tCredManr);
  CredManr.LoanApplication = tCredManr.LoanApplication;
  CredManr.SerNr = NextSerNr("CredManVc",CredManr.startDate,-1,false,"");
  CredManr.OKFlag = 1;
  CredManr.Approved = 1;
  RecordInsert(CredManr,true);

  return;
end;

global
updating procedure CreditAndOKInvoice(Longint ivnr,val prc,var val credval,var vector val vCredVal,val rval,Boolean sellf,Date td,string artcode,string findstr,string overrideperiod)
begin
  record IVVc IVr,IVCreditr,oldIVr,IV2r;
  string 255 warning;
  Integer i,rwcnt;
  row IVVc IVrw;
  val ratio;
  record LocalMachineBlock LMb;
  Integer err1,err2;

  IVr.SerNr = ivnr;
  if (ReadFirstMain(IVr,1,true)) then begin
    ratio = rval/IVr.Sum4;
    if (CreateCreditNoteIV(IVr,kInvoiceTypeCredit,IVCreditr,"",false)==0) then begin
      if (nonblank(td)) then begin
        IVCreditr.InvDate = td;
        IVVc_PasteInvDate(IVCreditr,LMb,err1,err2);
      end;
      if (IVr.InvDate>IVCreditr.InvDate) then begin
        IVCreditr.InvDate = IVr.InvDate;
        IVVc_PasteInvDate(IVCreditr,LMb,err1,err2);
      end;
      IVCreditr.NoEmailFlag = 1;
      IVCreditr.ExcludeFromExports = 1;
      IVCreditr.CustCredManNr = IVr.CustCredManNr;
      IVCreditr.SerNr = NextSerNr("IVVc",IVCreditr.InvDate,-1,false,"");
      rwcnt = MatRowCnt(IVCreditr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(IVCreditr,i,IVrw);
        if ((blank(artcode) or artcode==IVrw.ArtCode) and nonblank(IVrw.ArtCode)) then begin
          if (IVrw.Price>0) then begin
            if (ratio!=1) then begin
              IVrw.Price = IVrw.Price * ratio;
              MatRowPut(IVCreditr,i,IVrw);
              IVVc_PastePrice(IVCreditr,IV2r,i,warning);
              MatRowGet(IVCreditr,i,IVrw);
            end;
            if (nonblank(artcode)) then begin
              IVrw.Price = rval;
              MatRowPut(IVCreditr,i,IVrw);
              IVVc_PastePrice(IVCreditr,IV2r,i,warning);
              MatRowGet(IVCreditr,i,IVrw);
            end;
            if (sellf) then begin
              IVrw.PeriodCode = "";
            end;
            MatRowPut(IVCreditr,i,IVrw);
            credval = credval + IVrw.Sum;

/* not used, because we can't change items on credit note
            if (nonblank(replaceitem) and IVrw.ChargeType!=1) then begin
              IVrw.ArtCode = replaceitem;
              IVrw.Spec = replaceitemname;
              MatRowPut(IVCreditr,i,IVrw);
            end;
*/
            vCredVal[IVrw.ArtCode] = vCredVal[IVrw.ArtCode] + IVrw.Sum;
          end;
        end else begin
          if (blank(IVrw.ArtCode) and nonblank(findstr)) then begin
            if (IVrw.Spec==findstr) then begin
              IVrw.Spec = overrideperiod;
              MatRowPut(IVCreditr,i,IVrw);
            end;
          end;
          if (nonblank(IVrw.ArtCode) and nonblank(artcode)) then begin
            MatRowDelete(IVCreditr,i);
            i = i - 1;
            rwcnt = MatRowCnt(IVCreditr);
          end;
        end;
      end;
      if (RecordInsert(IVCreditr,true)) then begin
        RecordCopy(oldIVr,IVCreditr);
        IVCreditr.OKFlag = 1;
        RecordUpdate(oldIVr,IVCreditr,true);
      end;
    end;
  end;

  return;
end;

procedure GetItemName(string code,var string spec)
begin
  record INVc INr;

  INr.Code = code;
  ReadFirstMain(INr,1,true);
  spec = INr.Name;

  return;
end;

updating procedure CreditCredManInvoices(record CredManVc CredManr,val prc,var val credval,var vector val vCredVal,Boolean sellf,Date td)
begin
  Boolean TrHs;
  record CredHistVc CredHistr;
  vector Boolean vInv;
  array string 255 invarr;
  record ARVc ARr;
  Integer i;
  record CMInvoicingBlock CIb;

  credval = 0;
  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  while (LoopKey("CredManChargeType",CredHistr,1,TrHs)) begin
    if (CredHistr.CredManNr!=CredManr.SerNr) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==0) then begin
        vInv[CredHistr.RecordNr] = true;
      end;
    end;
  end;

  GetVectorTags(vInv,invarr);
  for (i=0;i<invarr.length;i=i+1) begin
    ARr.InvoiceNr = StringToLongint(invarr[i]);
    if (ReadFirstMain(ARr,1,true)) then begin
      if (ARr.RVal>0) then begin
        CreditAndOKInvoice(ARr.InvoiceNr,prc,credval,vCredVal,ARr.RVal,sellf,td,"","","");
      end;
    end;
  end;
  
  return;
end;

global
updating function Boolean DoRefinCredMan(record RcVc RepSpec,var string msg,var record CredManVc nCredManr)
begin
  record CMOrderClassBlock OCb;
  record IVVc IVr;
  record CredManVc CredManr,oldCredManr;
  Boolean res;
  val credval;
  vector val vCredVal;

  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    BlockLoad(OCb);
    RecordCopy(oldCredManr,CredManr);
    CredManr.OrderClass = OCb.RefinClass;
    if (RecordUpdate(oldCredManr,CredManr,true)==0) then begin
      CreateRefinCredMan(RepSpec,nCredManr,CredManr);
      DoBuyOutAgreement(CredManr,CurrentDate,false,IVr);
      CreditCredManInvoices(CredManr,100,credval,vCredVal,false,"");
      res = true;
    end;
  end;

  DoRefinCredMan = res;
  return;
end;

global
procedure GetFactoringRecord(record CredManVc CredManr,var record CredManFactVc CMFr)
begin
  record CredManFactVc tCMFr;

  tCMFr.CredManNr = CredManr.SerNr;
  if (ReadFirstMain(tCMFr,1,true)) then begin
    RecordCopy(CMFr,tCMFr);
  end else begin
    RecordNew(CMFr);
    CMFr.CredManNr = CredManr.SerNr;
  end;

  return;
end;

global
procedure GetPayGrntRecord(record CredManVc CredManr,var record CredManPmtGrntVc PGr)
begin
  record CredManPmtGrntVc tPGr;

  tPGr.CredManNr = CredManr.SerNr;
  if (ReadFirstMain(tPGr,1,true)) then begin
    RecordCopy(PGr,tPGr);
  end else begin
    RecordNew(PGr);
    PGr.CredManNr = CredManr.SerNr;
  end;

  return;
end;

function Integer GetSuretyCnt(record CredManVc CredManr)
begin
  Integer res;
  
  res = 0;
  if (nonblank(CredManr.SuretyProvider1)) then begin
    res = res + 1;
  end;
  if (nonblank(CredManr.SuretyProvider2)) then begin
    res = res + 1;
  end;
  if (nonblank(CredManr.SuretyProvider3)) then begin
    res = res + 1;
  end;

  GetSuretyCnt = res;
  return;
end;

procedure AddCustToDocument(var record AgreementDocVc ADr,string code,string name)
begin
  row AgreementDocVc ADrw;
  
  ClearRow(ADr,ADrw,1);
  ADrw.CustCode = code;
  ADrw.CustName = name;
  MatRowPut(ADr,MatRowCnt(ADr),ADrw);

  return;
end;

function Boolean CustIsCEO(string custcode,record CredManVc CredManr)
begin
  Boolean res,foundf;
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;

  if (ReadRecordLink(CredManr,1,LAr,RLr)) then begin
    foundf = true;
  end else begin
    if (CredManr.LoanApplication>0) then begin
      LAr.SerNr = CredManr.LoanApplication;
      if (ReadFirstMain(LAr,1,true)) then begin
        foundf = true;
      end;
    end;
  end;
  if (foundf) then begin
    if (LAr.CEOCustCode==custcode) then begin
      res = true;
    end;
  end;  

  CustIsCEO = res;
  return;
end;


procedure AddSuretyToDocument(var record AgreementDocVc ADr,record CredManVc CredManr)
begin
  Integer i;
  string 255 custcode;
  row AgreementDocVc ADrw;
   
  for (i=1;i<4;i=i+1) begin
    custcode = GetFieldValueByName(CredManr,"SuretyProvider" & i,-1);
    if (nonblank(custcode)) then begin
      if (CustIsCEO(custcode,CredManr)==false) then begin
        AddCustToDocument(ADr,custcode,GetFieldValueByName(CredManr,"Surety" & i & "Name",-1))
      end;
    end;
  end;

  return;
end;

procedure AddSimplifiedSuretyToDocument(var record AgreementDocVc ADr,record CredManVc CredManr,Integer num)
begin
  Integer i;
  string 255 custcode;
  row AgreementDocVc ADrw;
   
  custcode = GetFieldValueByName(CredManr,"SuretyProvider" & num,-1);
  if (nonblank(custcode)) then begin
    if (CustIsCEO(custcode,CredManr)==false) then begin
      AddCustToDocument(ADr,custcode,GetFieldValueByName(CredManr,"Surety" & num & "Name",-1))
    end;
  end;

  return;
end;

procedure GetDocumentSigners(Integer type,record CredManVc CredManr,var Boolean flag1,var Boolean flag2,var Boolean flag3)
begin
  record CredManTypeVc CMTr;
  record CUVc CUr;
  
  CMTr.Code = CredManr.Type;
  if (ReadFirstMain(CMTr,1,true)) then begin
    switch (type) begin
      case 1: 
        flag1 = CMTr.CredManCEO1==1; flag2 = CMTr.CredManCEO2==1; flag3 = CMTr.CredManSurety==1;
      case 3: 
        flag1 = CMTr.SuretyCEO1==1; flag2 = CMTr.SuretyCEO2==1; flag3 = CMTr.SuretySurety==1;
        if (HasSuretyPerson) then begin
          CUr.Code = CredManr.CustCode;
          if (ReadFirstmain(CUr,1,true))then begin
            if (CUr.CustType==1) then begin
              flag1 = CMTr.SuretyPersonCEO1==1; flag2 = CMTr.SuretyPersonCEO2==1; flag3 = CMTr.SuretyPersonSurety==1;
            end;
          end;
        end;
      case 2: 
        flag1 = CMTr.ScheduleCEO1==1; flag2 = CMTr.ScheduleCEO2==1; flag3 = CMTr.ScheduleSurety==1;
      case 6: 
        if (CMTr.VekselisShortMonths>0 and CredManr.InstalmentMonths<CMTr.VekselisShortMonths) then begin
          flag1 = CMTr.VekselisShortCEO1==1; flag2 = CMTr.VekselisShortCEO2==1; flag3 = CMTr.VekselisShortSurety==1;
        end else begin
          flag1 = CMTr.VekselisCEO1==1; flag2 = CMTr.VekselisCEO2==1; flag3 = CMTr.VekselisSurety==1;
        end;
    end;
  end;

  return;
end;

global
function Boolean CustIsSurety(string custcode,record CredManVc CredManr)
begin
  Boolean res;
  
  if (GetFieldValueByName(CredManr,"SuretyProvider1",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;
  if (GetFieldValueByName(CredManr,"SuretyProvider2",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;
  if (GetFieldValueByName(CredManr,"SuretyProvider3",-1)==custcode) then begin
    res = true;
    goto LCustIsSurety;
  end;

LCustIsSurety:;
  CustIsSurety = res;
  return;
end;

procedure AddCEOToDocument(record AgreementDocVc ADr,record CredManVc CredManr,Boolean suretyf)
begin
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;
  Boolean testf,foundf;
  record CUVc CUr;

  if (ReadRecordLink(CredManr,1,LAr,RLr)) then begin
    foundf = true;
  end else begin
    if (CredManr.LoanApplication>0) then begin
      LAr.SerNr = CredManr.LoanApplication;
      if (ReadFirstMain(LAr,1,true)) then begin
        foundf = true;
      end;
    end;
  end;
  if (foundf) then begin
    if (nonblank(LAr.CEOCustCode)) then begin
      testf = CustIsSurety(LAr.CEOCustCode,CredManr);
      if (suretyf and testf) then begin
        AddCustToDocument(ADr,LAr.CEOCustCode,LAr.CEOName);
      end;
      if (!suretyf and !testf) then begin
        AddCustToDocument(ADr,LAr.CEOCustCode,LAr.CEOName);
      end;
    end;
  end else begin
    if (FindCEOContact(CredManr.CustCode,CUr)) then begin
      testf = CustIsSurety(CUr.Code,CredManr);
      if (suretyf and testf) then begin
        AddCustToDocument(ADr,CUr.Code,CUr.Name);
      end;
      if (!suretyf and !testf) then begin
        AddCustToDocument(ADr,CUr.Code,CUr.Name);
      end;
    end;
  end;

  return;
end;

function string 255 GetAgreementDocumentName(record AgreementDocVc ADr,string base,Boolean regenf)
begin
  string 255 res;

  res = base & ADr.RecordNr;
  if (regenf) then begin
    res = res & "_new";
    res = res & DateToString(CurrentDate,"YYYYMMDD");
  end;
  res = res & ".pdf";

  GetAgreementDocumentName = res;
  return;
end;

updating procedure PrintSingleAgreementDoc(record AgreementDocVc ADr,record CredManVc CredManr,Boolean regenf)
begin
  string 255 fn;
  record RcVc RepSpec;
  string 255 langcode,oldname;
  Integer scnt;
  area filearea;
  transaction Longint gSuretyNumber;
  transaction Longint gBOENumber;
  transaction integer gIsDigitalDocument;
  Boolean skipf;
  val paidout;
  
  ArchiveExistingDocument(ADr,oldname);
  if(ADr.DigitalFlag==1)then begin
    gIsDigitalDocument = 1;
  end;
  scnt = GetSuretyCnt(CredManr);
  switch (ADr.Type) begin
    case 1://agreement
      fn = GetAgreementDocumentName(ADr,"Agreement",regenf);
      langcode = GetDocumentLangCode(CredManr,1,-1);
      if (ExternalDocumentEnabled("CredManForm")) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
        PrintEBSDocument("CredManForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
      end else begin
        PrintCredManDocument("CredManForm",fn,CredManr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        //PrintMultiDocDocument("CredManLongForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
    case 2://schedule
      fn = GetAgreementDocumentName(ADr,"Schedule",regenf);
      if (GetInterestPaymentType(CredManr)==2) then begin
        AgreementPaidOut2(CredManr,paidout);
        if (paidout==0) then begin
          skipf = true;
        end;
      end;
      if (skipf==false) then begin
        GenerateAgreementSchedule(CredManr,false,true);
      end;
      langcode = GetDocumentLangCode(CredManr,2,-1);
      if (ExternalDocumentEnabled("CredManScheduleForm")) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
        PrintEBSDocument("CredManScheduleForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
      end else begin
        PrintCredManDocument("CredManScheduleForm",fn,CredManr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
        //PrintMultiDocDocument("CredManScheduleForm",fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
    case 3://surety
      fn = GetAgreementDocumentName(ADr,"Surety",regenf);
      langcode = GetDocumentLangCode(CredManr,3,-1);
      if (ExternalDocumentEnabled("SuretyAgreementForm")) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
        RepSpec.flags[11] = scnt;
        PrintEBSDocument("SuretyAgreementForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
      end else begin
        PrintCredManDocument("SuretyAgreementForm",fn,CredManr,scnt,(CredManr.MaxMonthlyPayment>0),langcode);
        //PrintMultiDocDocument("SuretyAgreementForm" & scnt,fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
    case 6://vekselis
      fn = GetAgreementDocumentName(ADr,"Bill",regenf);
      langcode = GetDocumentLangCode(CredManr,4,-1);
      if (ExternalDocumentEnabled("VekselisForm")) then begin
        RepSpec.long1 = CredManr.InstalmentType;
        if (CredManr.MaxMonthlyPayment>0) then begin
          RepSpec.flags[10] = 1;
        end;
          RepSpec.flags[11] = scnt;
          PrintEBSDocument("VekselisForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
      end else begin
        PrintCredManDocument("VekselisForm",fn,CredManr,scnt,(CredManr.MaxMonthlyPayment>0),langcode);
        //PrintMultiDocDocument("VekselisForm" & scnt,fn,CredManr.SerNr,CredManr.InstalmentType,(CredManr.MaxMonthlyPayment>0),langcode);
      end;
    otherwise//surety simplified 7-12
      if (ADr.Type>8 and ADr.Type<15) then begin
        fn = GetAgreementDocumentName(ADr,"Surety",regenf);
        langcode = GetDocumentLangCode(CredManr,3,ADr.Type-8);
        if (ExternalDocumentEnabled("SuretyAgreementForm")) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          gSuretyNumber = ADr.Type-8;
          PrintEBSDocument("SuretyAgreementForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
          gSuretyNumber = -1;
        end else begin
          PrintCredManDocument("SuretyAgreementForm",fn,CredManr,1,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
      end;
      if (ADr.Type>15 and ADr.Type<20) then begin//bill of exchange
        fn = GetAgreementDocumentName(ADr,"Bill",regenf);
        langcode = GetDocumentLangCode(CredManr,4,ADr.Type-15);
        if (ExternalDocumentEnabled("VekselisForm")) then begin
          RepSpec.long1 = CredManr.InstalmentType;
          if (CredManr.MaxMonthlyPayment>0) then begin
            RepSpec.flags[10] = 1;
          end;
          gBOENumber = ADr.Type-15;
          PrintEBSDocument("VekselisForm",fn,RepSpec,CredManr.SerNr,langcode,filearea);
          gBOENumber = -1;
        end else begin
          PrintCredManDocument("VekselisForm",fn,CredManr,scnt,(CredManr.MaxMonthlyPayment>0),langcode);
        end;
      end;
  end;
  RecordLinkFile(fn,0,ADr,CurrentCompany);
  Delete_File(fn);

  return;
end;

global
updating procedure CreateAgreementDoc(record CredManVc CredManr,Integer type,var record AgreementDocVc ADr)
begin
  string 255 fn,langcode;
  Integer scnt;
  row AgreementDocVc ADrw;
  area filearea;
  record RcVc RepSpec;
  Boolean flags1,flags2,flags3;
  row CredLegalInvNrBlock LegalInvNrrw;
  record CredManVc oldCredManr;

  RecordNew(ADr);
  ADr.SerNr = NextSerNr("AgreementDocVc",CurrentDate,-1,false,"");
  ADr.TransDate = CurrentDate;
  ADr.TransTime = CurrentTime;
  ADr.RecordNr = CredManr.SerNr;
  ADr.Type = type;
  
  GetDocumentSigners(ADr.Type,CredManr,flags1,flags2,flags3);
  if (flags1) then begin
    AddCEOToDocument(ADr,CredManr,false);    
  end;
  if (flags2) then begin
    AddCEOToDocument(ADr,CredManr,true);
  end;
  if (type>8 and type<15) then begin
      AddSimplifiedSuretyToDocument(ADr,CredManr,type-8);    
  end else begin
    if (flags3) then begin
      AddSuretyToDocument(ADr,CredManr);    
    end;
  end;
  if (RecordInsert(ADr,true)) then begin
    PrintSingleAgreementDoc(ADr,CredManr,false);
    if (blank(CredManr.OfficialSerNr)) then begin
      if (GetCredLegalNrLine("CredManVc",CredManr.SerNr,CredManr.startDate,CredManr.Type,CredManr.Classifications,LegalInvNrrw,3)) then begin
        RecordCopy(oldCredManr,CredManr);
        CredManr.OfficialSerNr = NextLegalSerNr("CredManVc",CredManr.SerNr,CredManr.startDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
        RecordUpdate(oldCredManr,CredManr,true);
      end;
    end;

  end;

  return;
end;

global
function Boolean GetAgreementDoc(record CredManVc CredManr,Integer type,var record AgreementDocVc ADr)
begin
  record AgreementDocVc tADr;
  Boolean res;
  
  tADr.RecordNr = CredManr.SerNr;
  tADr.RecordType = 0;
  tADr.Type = type;
  if (ReadFirstKey("RecordNr",tADr,3,true)) then begin
    RecordCopy(ADr,tADr);
    res = true;
  end else begin
    qupdating.CreateAgreementDoc(CredManr,type,ADr);
    res = true;
  end;

  GetAgreementDoc = res;
  return;
end;

global
updating procedure CreateAgreementDoc2(Longint sernr,string custcode,Integer rw,var record AgreementDocVc ADr)
begin
  string 255 fn;
  Integer scnt;
  row AgreementDocVc ADrw;
  record B2BLoanApplicationVc LAr;
  record RLinkVc RLr;
  record RcVc RepSpec;
/*
  RecordNew(ADr);
  ADr.SerNr = NextSerNr("AgreementDocVc",CurrentDate,-1,false,"");
  ADr.TransDate = CurrentDate;
  ADr.TransTime = CurrentTime;
  ADr.RecordNr = sernr;
  ADr.Type = 8;
  if (RecordInsert(ADr,true)) then begin
    fn = "PaymentCertificate.pdf";
    RepSpec.ArtMode = rw;
    PrintMultiDocDocument2("PaymentCertificateForm",fn,sernr,0,false,"",RepSpec);
    RecordLinkFile(fn,0,ADr,CurrentCompany);
    Delete_File(fn);
  end;
*/

  return;
end;

global
function Boolean GetAgreementDoc2(Longint sernr,string custcode,Integer rw,var record AgreementDocVc ADr)
begin
  record AgreementDocVc tADr;
  Boolean res,TrHs;
  
  TrHs = true;
  tADr.RecordNr = sernr;
  tADr.RecordType = 0;
  tADr.Type = 8;
  while (LoopKey("RecordNr",tADr,3,TrHs)) begin
    if (tADr.RecordNr!=sernr or tADr.RecordType!=0 or tADr.Type!=8) then begin
      TrHs = false;
    end else begin
      if (ADr.CustCode==custcode) then begin
        RecordCopy(ADr,tADr);
        res = true;
        goto LGetAgreementDoc2;
      end;
    end;
  end;
  qupdating.CreateAgreementDoc2(sernr,custcode,rw,ADr);
  res = true;//no point of having a function i guess

LGetAgreementDoc2:;
  GetAgreementDoc2 = res;
  return;
end;

updating procedure ResetSignatureStatus(var record AgreementDocVc ADr,record CredManVc CredManr)
begin
  record AgreementDocVc oldADr;
  row AgreementDocVc ADrw;
  Integer i,rwcnt;
  record CMApplicationSetBlock CMb;
  record CredManVc oldCredManr;
  
  RecordCopy(oldADr,ADr);

  ADr.SignedByUs = 0;
  ADr.SignedByUsDate = "";
  ADr.SignedByUsTime = "";

  rwcnt = MatRowCnt(ADr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ADr,i,ADrw);
    ADrw.Status = 0;
    ADrw.SignatureDate = "";
    ADrw.SignatureTime = "";
    MatRowPut(ADr,i,ADrw);
  end;

  if (RecordUpdate(oldADr,ADr,true)==0) then begin
    RecordCopy(oldCredManr,CredManr);
    switch (ADr.Type) begin
      case 1: CredManr.SignAgreement = 0;
      case 2: CredManr.SignSchedule = 0;
      case 3: CredManr.SignSurety = 0;
      case 6: CredManr.SignVekselis = 0;
      otherwise
        if (ADr.Type>8 and ADr.Type<15) then begin
          SetFieldValueByName(CredManr,"Surety" & (ADr.Type-8) & "Signed",-1,0);
        end;
        if (ADr.Type>15 and ADr.Type<20) then begin
          SetFieldValueByName(CredManr,"BOE" & (ADr.Type-15) & "Signed",-1,0);
        end;
    end;

    BlockLoad(CMb);
    if (nonblank(CMb.SignOrderClass) and CredManr.OrderClass!=CMb.SignOrderClass) then begin
      CredManr.OrderClass = CMb.SignOrderClass;
    end;

    RecordUpdate(oldCredManr,CredManr,true);
  end;

  return;
end;

global
updating procedure RecreateAgreementDocument(var record AgreementDocVc ADr,Boolean resignf)
begin
  record CredManVc CredManr;
  
  if (ADr.RecordType==0) then begin
    CredManr.SerNr = ADr.RecordNr;
    if (ReadFirstMain(CredManr,1,true)) then begin
      PrintSingleAgreementDoc(ADr,CredManr,true);
      if (resignf) then begin
        ResetSignatureStatus(ADr,CredManr);
      end;
    end;
  end;

  return;
end;


updating procedure CredManCreditAllInvoices(record CredManVc CredManr)
begin
  record ARVc ARr;
  Boolean TrHs;
  record IVVc IVr,oldIVr,IVCreditr;
  record LocalMachineBlock LMb;
  Integer err1,err2;
  array record IVVc aIVr;
  Integer i;

  ARr.CustCode = CredManr.CustCode;
  TrHs = true;
  while (LoopKey("CustCode",ARr,1,TrHs)) begin
    if (ARr.CustCode!=CredManr.CustCode) then begin
      TrHs = false;
    end else begin
      if (ARr.RVal>0) then begin
        IVr.SerNr = ARr.InvoiceNr;
        if (ReadFirstMain(IVr,1,true)) then begin
          aIVr[aIVr.length] = IVr;
        end;
      end;
    end;
  end;
  for (i=0;i<aIVr.length;i=i+1) begin
    IVr = aIVr[i];
    RecordClear(IVCreditr);
    if (CreateCreditNoteIV(IVr,kInvoiceTypeCredit,IVCreditr,"",false)==0) then begin
      if (IVr.InvDate>CurrentDate) then begin
        IVCreditr.InvDate = IVr.InvDate;   
        IVVc_PasteInvDate(IVCreditr,LMb,err1,err2);
      end;
      IVCreditr.SerNr = NextSerNr("IVVc",IVCreditr.InvDate,-1,false,"");
      if (RecordInsert(IVCreditr,true)) then begin
        RecordCopy(oldIVr,IVCreditr);
        IVCreditr.OKFlag = 1;
        RecordUpdate(oldIVr,IVCreditr,true);
      end;
    end;
  end;
 

  return;
end;

global
updating procedure CreateCredmanPurchaseInvoice(record CredManVc CredManr,val credsum,var record VIVc VIr,val overridesum,Boolean extrapayment,Date overridedate)
begin
  record CMInvoicingBlock CIb;
  record VIVc oldVIr;
  row VIVc VIrw;
  record CredManAccVc CMAr;
  val ct;
  string 255 warning,tstr;
  val sum;

  sum = CredManr.InvSum4;
  if (overridesum>0) then begin
    sum = overridesum;
  end;

  BlockLoad(CIb);
  RecordNew(VIr);
  if (CredManr.LoanType==7 or CredManr.LoanType==8) then begin
    VIr.TransDate = CurrentDate;
  end else begin
    VIr.TransDate = CredManr.startDate;
  end;
  if (nonblank(overridedate)) then begin
    VIr.TransDate = overridedate;
  end;
  VIr.InvDate = VIr.TransDate;
  VIr.SerNr = NextSerNr("VIVc",VIr.InvDate,-1,false,"");
  VIVc_PasteInvDate(VIr);    
  VIr.VECode = CredManr.CustCode;
  VIVc_PasteVECode(VIr,0,true,true,warning)
  VIr.PayDeal = CIb.PaymentPayDeal;
  VIVc_PastePayDeal(VIr);
  VIr.Objects =  AddObjectToObjectList(VIr.Objects,CIb.PaymentObject);
  if (FindCredManAcc(CredManr,CMAr)) then begin
    VIr.Objects = AddObjectToObjectList(VIr.Objects,CMAr.Objects);
  end;
  VIr.Objects = AddObjectToObjectList(VIr.Objects,CredManr.Objects);
  VIr.PayVal = sum;
  VIr.CustCredManNr = CredManr.SerNr;
  if (extrapayment) then begin
    VIr.CredManExtraPayment = 1;
  end;
  ClearRow(VIr,VIrw,1);
  tstr = CM_GetCredManTypePurchItemAccount2(CredManr,CIb.PaymentAccount,true);
  VIrw.AccNumber = tstr;

  MatRowPut(VIr,0,VIrw);
  VIVc_PasteAccNumber(VIr,"",true,0);
  MatRowGet(VIr,0,VIrw);
  VIrw.VATCode = CIb.PaymentVATCode;
  MatRowPut(VIr,0,VIrw);
  VIDDefault(0,"VATCode",VIr);
  MatRowGet(VIr,0,VIrw);
  VIrw.Sum = sum;
  MatRowPut(VIr,0,VIrw);
  VICalcVals(VIr);
  VISumup(VIr,ct);
  if (RecordInsert(VIr,true)) then begin
    RecordCopy(oldVIr,VIr);
    VIr.OKFlag = 1;
    RecordUpdate(oldVIr,VIr,true);
    CreateRecordLink(CredManr,CurrentCompany,VIr,CurrentCompany);
    CreateRecordLink(VIr,CurrentCompany,CredManr,CurrentCompany);
  end; 

  return;
end;

global
updating procedure CreatePaymentFakeReceipt(record CredManVc CredManr,record IVVc IVr)
begin
  val chk;
  Boolean installmentf;
  record IPVc IPr,oldIPr;
  row IPVc IPrw;
  record CMInvoicingBlock CIb;
  vector Boolean invused;
  Integer k;
  val remsum;
  Integer rownr;

  BlockLoad(CIb);
  IPr.TransDate = IVr.InvDate;
  IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
  IPr.RegDate = IVr.InvDate;
  IPr.OKFlag = 0;
  IPr.PayMode = CIb.PaymentMode;
  IPPastePayMode(IPr);


  remsum = IVr.Sum4;
  //GetCustomerLoanAmounts2(IPr,k,IVr.Addr0,IVr.CurncyCode,remsum,IVr.CustCode,true,"","",invused,IVr.InvDate,CredManr.SerNr);

/*
  ClearRow(IPr,IPrw,1);
  IPrw.InvoiceNr = IVr.SerNr;
  MatRowPut(IPr,0,IPrw);

  PasteInvIn2IPr(IPr,0,IPr.TransDate,chk,false,installmentf);
*/
  rownr = 0;
  ClearRow(IPr,IPrw,1);
  IPrw.InvoiceNr = IVr.SerNr;
  MatRowPut(IPr,rownr,IPrw);
  PasteInvIn2IPr(IPr,rownr,IPr.TransDate,chk,false,installmentf);
  DistributeCredHistValues(IPr,rownr);

  if (RecordStore(IPr,true)) then begin
    RecordCopy(oldIPr,IPr);
    IPr.OKFlag = 1;
    RecordUpdate(oldIPr,IPr,true);
  end;

  return;
end;

global
updating procedure CM_CreatePaymentOrder(record CredManVc CredManr,record VIVc VIr,val amount)
begin
  record OPVc OPr,oldOPr;
  row OPVc OPrw;
  val chk;
  Boolean installmentf;
  string 255 warning;
  record CMInvoicingBlock CIb;

  BlockLoad(CIb);
  RecordNew(OPr);
  OPr.TransDate = VIr.InvDate;	 
  OPr.PayDate = VIr.InvDate;	 
  OPr.SerNr = NextSerNr("OPVc",OPr.TransDate,-1,false,"");		
  OPr.PayMode = CIb.PaymentMode;
  OPPastePayMode(OPr);

  ClearRow(OPr,OPrw,1);
  OPrw.VISerNr = VIr.SerNr;
  MatRowPut(OPr,0,OPrw);
  PasteInvIn2OPr(OPr,0,OPr.TransDate,1,chk,warning,false,installmentf);
  MatRowGet(OPr,0,OPrw);
  OPrw.BankVal = amount;
  OPrw.RecVal = amount;
  MatRowPut(OPr,0,OPrw);
  OPVc_PasteRecVal(OPr,0);

  if (RecordStore(OPr,true)) then begin
    RecordCopy(oldOPr,OPr);
    OPr.DoneFlag = 1;
    OPr.OrderedFlag = 1;
    RecordUpdate(oldOPr,OPr,true);
  end;

  return;
end;

global
updating procedure CredManMakePaymentRemote(var record CredManVc CredManr,var record VIVc VIr,var record IVVc tIVr,string invno,var val sum,Boolean receiptf,val overridesum)
begin
  record IVVc IVr;
  record CMOrderClassBlock OCb;
  record CMInvoicingBlock CIb;
  record CredManVc tCredManr,oldCredManr;
  Boolean updf;


  if (CredManr.InstalmentType!=4 and CredManr.InstalmentType!=5) then begin
    CreateSingleAgreementInvoice2(CredManr,AddDay(CredManr.FirstInvDate,-1),IVr,invno,receiptf,false);//marking it as manual only if we make a receipt
    CheckAndCreateAutoMailFromInvoice(IVr);
    RecordCopy(tIVr,IVr);
    
    //CredManCreditAllInvoices(CredManr);
    CreateCredmanPurchaseInvoice(CredManr,IVr.Sum4,VIr,overridesum,false,"");
    tCredManr.SerNr = CredManr.SerNr;
    if (ReadFirstMain(tCredManr,1,true)) then begin
      if (ComparePosts(tCredManr,CredManr)==false) then begin
        updf = true;
        RecordCopy(CredManr,tCredManr);
        RecordCopy(oldCredManr,CredManr);
      end;
    end;
    sum = IVr.Sum4;
    if (VIr.SerNr>0) then begin
      BlockLoad(OCb);
      CredManr.VINr = VIr.SerNr;
      CredManr.OrderClass = OCb.ActiveCredOrderClass;
      BlockLoad(CIb);
      if (CIb.MoneyTransferDateVI==1) then begin
        CredManr.MoneyTransferDate = VIr.InvDate;
      end;
      if (updf) then begin
        if (RecordUpdate(oldCredManr,CredManr,true)==0) then begin
        end;
      end;
    end;
  end;


  return;
end;

function string 255 GetDefaultPayMode()
begin
  record PMBlock PMb;
  row PMBlock PMbrw;
  string 255 res;

  BlockLoad(PMb);
  MatRowGet(PMb,0,PMbrw);
  res = PMbrw.Code;

  GetDefaultPayMode = res;
  return;
end;

updating procedure UnlinkCredManInvoice(record IVVc IVr)
begin
  record CredHistVc CredHistr;
  Boolean TrHs;
  record IVVc oldIVr;
  row IVVc IVrw;
  Integer i,rwcnt;

  TrHs = true;
  CredHistr.RecordNr = IVr.SerNr;
  CredHistr.RecordType = 0;
  while (LoopKey("RecordNr",CredHistr,2,TrHs)) begin
    if (CredHistr.RecordNr!=IVr.SerNr or CredHistr.RecordType!=0) then begin
      TrHs = false;
    end else begin
      RecordRemove(CredHistr);
      StepBack(CredHistr);
    end;
  end;

  RecordCopy(oldIVr,IVr);
  IVr.CustCredManNr = -1;
  rwcnt = MatRowCnt(IVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVr,i,IVrw);
    IVrw.ChargeType = 0;
    IVrw.CredHistNr = -1;
    MatRowPut(IVr,i,IVrw);
  end;
  RecordUpdate(oldIVr,IVr,false);

  return;
end;

updating procedure ReturnInvoiceAndMakePrepaymentSeparate(record IVVc IVr)
begin
  row IPVc IPrw;
  Integer rownr;
  val chk;
  Boolean installmentf;
  Longint err;
  record ARVc ARr;
  val sum;
  record IPVc oldIPr;
  record IPVc IPr;

  sum = IVr.Sum4;
  ARr.InvoiceNr = IVr.SerNr;
  if (ReadFirstMain(ARr,1,true)) then begin
    if (ARr.RVal>=0) then begin
      sum =   IVr.Sum4 - ARr.RVal;
    end;
  end;

  if (sum>0) then begin
    RecordNew(IPr);
    IPr.TransDate = CurrentDate;
    IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
    IPr.RegDate = CurrentDate;
    IPr.OKFlag = 0;
    IPr.PayMode = GetReceiptsPayMode(IVr);
    IPPastePayMode(IPr);
    rownr = 0;
    ClearRow(IPr,IPrw,1);
    IPrw.InvoiceNr = IVr.SerNr;
    MatRowPut(IPr,rownr,IPrw);
    PasteInvIn2IPr(IPr,rownr,IPr.TransDate,chk,false,installmentf);
    MatRowGet(IPr,rownr,IPrw);
    IPrw.RecVal = -sum;
    MatRowPut(IPr,rownr,IPrw);
    IPVc_PasteRecVal(IPr,rownr);

    IPSumup(IPr);
    if (RecordInsert(IPr,true)) then begin
      RecordCopy(oldIPr,IPr);
      IPr.OKFlag = 1;
      RecordUpdate(oldIPr,IPr,true);
    end;
    
    RecordClear(IPr);
    RecordNew(IPr);
    IPr.TransDate = AddDay(IVr.InvDate,-1);
    IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
    IPr.RegDate = IPr.TransDate;
    IPr.OKFlag = 0;
    IPr.PayMode = GetReceiptsPayMode(IVr);
    IPPastePayMode(IPr);

    rownr = 0;
    ClearRow(IPr,IPrw,1);
    IPrw.CustCode = IVr.CustCode;
    MatRowPut(IPr,rownr,IPrw);
    IPVc_PasteCustCode(IPr,rownr,err);
    MatRowGet(IPr,rownr,IPrw);
    IPrw.RecVal = sum;
    IPrw.CUPNr = IPr.SerNr;
    MatRowPut(IPr,rownr,IPrw);
    IPVc_PasteRecVal(IPr,rownr);

    IPSumup(IPr);
    if (RecordInsert(IPr,true)) then begin
      RecordCopy(oldIPr,IPr);
      IPr.OKFlag = 1;
      RecordUpdate(oldIPr,IPr,true);
    end;

  end;
  return;
end;

updating function Boolean CredManClearIssuedInvoices(record CredManVc CredManr,Date td)
begin
  val credval;
  vector val vCredVal;
  record IPVc IPr;
  Boolean res;
  record IVVc IVr;
  array record IVVc aIVr;
  vector Boolean vInv;
  record CredHistVc CredHistr;
  Integer i;
  
  CredHistr.TransDate = AddMonth(td,-1);  
  while (LoopKey("CredManTransDate:" & CredManr.SerNr,CredHistr,1,true)) begin
    if ((nonblank(CredHistr.CredToDate) and CredHistr.CredToDate>=td) or CredHistr.TransDate>=td) then begin
      if (CredHistr.RecordType==0 and CredHistr.MainRecord==1) then begin
        if (vInv[CredHistr.RecordNr]==false) then begin
          IVr.SerNr = CredHistr.RecordNr;
          if (ReadFirstMain(IVr,1,true)) then begin
            aIVr[aIVr.length] = IVr;
            vInv[CredHistr.RecordNr] = true; 
          end;
        end;
      end;
    end;
  end;
  for (i=0;i<aIVr.length;i=i+1) begin
    IVr = aIVr[i];
    RecordClear(IPr);
    ReturnInvoiceAndMakePrepaymentSeparate(IVr);
    UnlinkCredManInvoice(IVr);
    CreditAndOKInvoice(IVr.SerNr,100,credval,vCredVal,IVr.Sum4,false,CurrentDate,"","","");
    res = true;
  end;

  CredManClearIssuedInvoices = res;
  return;
end;

global
updating procedure CredManCreditBeforePartialBuyout(Integer wn,record RcVc RepSpec)
begin
  record CredManVc CredManr;
  Boolean res;

  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    res = CredManClearIssuedInvoices(CredManr,RepSpec.d1);
  end;
  
  ClientRemoteAsync.RefinCredManPartial2WClassSubmitCallBack(wn,RepSpec,res);

  return;
end;

global
updating procedure CredManPartialBuyoutDo(Date td,val buyoutval,Longint sernr,record RcVc RepSpec,Integer wn,Integer credmanwn,Boolean regenf)
begin
  record CredManVc CredManr,oldCredManr;
  record SMVc SMr;
  row SMVc SMrw;
  val tval;
  Longint histnr;
  record BaseCurBlock BCb;
  record AgreementDocVc ADr;
  string 255 err;
  Boolean updf;
  record IVVc IVr;
  Integer k;
  vector Boolean invused;
  record CMInvoicingBlock CIb;
  record CredManSetBlock CMb;
  Integer latedays;
  val lateamount;
  Date invdate,sd;
  val remsum;
  record IPVc IPr,oldIPr;
  row IPVc IPrw;
  Integer i,rwcnt;
  array string 255 lateformula;
  string 255 spec;
  Boolean fullf;
  record CredManPayPlanVc CredManPayPlanr;
  val balance;
  Date calcsd;

  CredManr.SerNr = sernr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    latedays = 0;
    lateamount = 0;
    invdate = td;
    BlockLoad(CMb);
    BlockLoad(CIb);
    fullf = UseFullBuyoutProcedure;
    if (RepSpec.flags[1]==1) then begin
      sd = GetLastInvoiceDate(CredManr,invdate);
      GetCredManOverdueInvoices(CredManr,CMb,invdate,sd,latedays,lateamount,lateformula,true,false,calcsd);
      if (latedays>0 or lateamount>0) then begin
        //Setup always new invoice
        SetupLoanInvoice(CredManr,CMb,IVr,invdate,calcsd,invdate,true,false,"",2,true);
        AddLateFees(CredManr,CMb,IVr,latedays,lateamount,lateformula);
        SetupOverdueLangCode(IVr,CredManr);
        StoreLoanInvoice(IVr,CredManr);
      end;
    end;
    if (RepSpec.flags[2]==1) then begin
      IPr.TransDate = td;
      IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
      IPr.RegDate = td;
      IPr.OKFlag = 0;
      IPr.PayMode = CIb.PaymentMode;
      IPPastePayMode(IPr);

      remsum = buyoutval;
      GetCustomerLoanAmounts2(IPr,k,CredManr.Addr0,CredManr.CurncyCode,remsum,CredManr.CustCode,true,"","",invused,td,CredManr.SerNr);
      
      rwcnt = MatRowCnt(IPr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(IPr,i,IPrw);
        if (IPrw.InvoiceNr>0) then begin
          tval = tval + IPrw.RecVal;
        end;
      end;
    end;

    if (buyoutval-tval<=0) then begin
      err = USetStr(200111);
      goto LCredManPartialBuyoutDo;
    end;
    RecordCopy(oldCredManr,CredManr);
    if (CredManr.VersionNo<0) then begin
      CredManr.VersionNo = 0;
    end;
    CredManr.VersionNo = CredManr.VersionNo + 1;
    if (RecordUpdate(oldCredManr,CredManr,true)==0) then begin
      updf = true;
    end;

    if (MatRowCnt(IPr)>0) then begin
      if (RecordStore(IPr,true)) then begin
        RecordCopy(oldIPr,IPr);
        IPr.OKFlag = 1;
        RecordUpdate(oldIPr,IPr,true);
      end;    
    end;

    //create a new invoice and creata receipt
    BlockLoad(CMb);
    SetupLoanInvoice2(CredManr,CMb,IVr,td,"","",true,false,"",1,true,CIb.BuyOutPayDeal);
    if (fullf==false) then begin
      IVr.OfficialSerNr = "";
      IVr.NoEmailFlag = 1;
      IVr.ExcludeFromExports = 1;
    end;
    GetItemName(CMb.InstalmentItem,spec);
    SetupInvoiceRowAndAdd(CredManr,IVr,CMb.InstalmentItem,spec,1,buyoutval-tval,blankval,19,-1);
    CheckAndAddHiddenPrinciple(IVr,CIb);
    //CM_AddPrepayments(IVr,CredManr,CIb);
    StoreLoanInvoice(IVr,CredManr);

    //IVr.Sum4 = buyoutval;//a little hack for this to work
    if (fullf) then begin
      CreatePaymentFakeReceipt(CredManr,IVr);
    end;

//    if (fullf==false) then begin
      SetupLoanInvoice2(CredManr,CMb,IVr,td,"","",true,false,"",1,true,CIb.BuyOutPayDeal);
      GetItemName(CMb.InstalmentItem,spec);
      AddBuyoutFee(CredManr,IVr,buyoutval-tval);
      StoreLoanInvoice(IVr,CredManr);
//    end;    
  end;
  
LCredManPartialBuyoutDo:;

  CheckBuyOutAgreementAsync(CredManr,CurrentDate,credmanwn);
  ClientRemoteAsync.RefinCredManPartialWClassSubmitCallBack(err,wn,credmanwn,regenf);
  return;
end;

global
procedure CredManPartialBuyoutFinish(Boolean regenivf,record CredManVc CredManr)
begin
  record IVVc tIVr;
  
  RecreateAgreementSchedule4(CredManr,true,false);
  if (regenivf) then begin
    qupdating.CreateSingleAgreementInvoice2(CredManr,CurrentDate,tIVr,"",false,true);
  end;

  return;
end;


global
updating procedure CredManCreditBeforePause(Integer wn,Integer mwn,record RcVc RepSpec)
begin
  record CredManVc CredManr;
  Boolean res

  CredManr.SerNr = RepSpec.long1;
  if (ReadFirstMain(CredManr,1,true)) then begin
    CredManClearIssuedInvoices(CredManr,RepSpec.d1);
  end;
  
  ClientRemoteAsync.CredManCreditBeforePauseCallBack(wn,mwn,RepSpec);

  return;
end;

global
procedure CredManDoPauseFinish(record CredManVc CredManr,Boolean fullf,Boolean invf)
begin
  record IVVc tIVr;
  
  RecreateAgreementSchedule4(CredManr,true,fullf);
  if (invf) then begin
    qupdating.CreateSingleAgreementInvoice2(CredManr,CurrentDate,tIVr,"",false,true);
  end;

  return;
end;

global
function Boolean ContractHasInvoiceCreated(record CredManVc CredManr)
begin
  record CredHistVc CredHistr;
  Boolean res;
  record CredManSetBlock CMSb;
  
  BLockLoad(CMSb);

  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.ArtCode = CMSb.RateItem;
  if (ReadFirstKey("ArtCode",CredHistr,2,true)) then begin
    res = true;
  end;

  ContractHasInvoiceCreated = res;
  return;
end;


global
procedure CredLegalNrSerNrSClassOnOpenWindowRemote(string filename,string sernr,Date invdate,string agrtype,string agrclass,var array string arrOffSerNr)
begin
  
  FindListCredLegalSerNr(filename,sernr,invdate,agrtype,agrclass,0,arrOffSerNr);

  return;
end;

global
updating procedure GetCredManPropertyRecord(record CredManVc CredManr,var record CredManPropertiesVc CMPr)
begin
  
  CMPr.CredManNr = CredManr.SerNr;
  CMPr.RecordType = 0;
  if (ReadFirstMain(CMPr,2,true)==false) then begin
    RecordNew(CMPr);
    CMPr.CredManNr = CredManr.SerNr;
    CMPr.RecordType = 0;
    RecordInsert(CMPr,true);
  end;

  return;
end;

global
updating procedure GetCredManBOESuretyRecord(record CredManVc CredManr,var record BOESuretyVc BSr,Integer num)
begin
  
  BSr.CredManNr = CredManr.SerNr;
  BSr.BOENum = num;
  if (ReadFirstMain(BSr,2,true)==false) then begin
    RecordNew(BSr);
    BSr.CredManNr = CredManr.SerNr;
    BSr.BOENum = num;
    RecordInsert(BSr,true);
  end;

  return;
end;

global
updating procedure GetCredManMoneyReceiverRecord(record CredManVc CredManr,var record CredManMoneyRecVc MRr)
begin
  
  MRr.CredManNr = CredManr.SerNr;
  if (ReadFirstMain(MRr,2,true)==false) then begin
    RecordNew(MRr);
    MRr.CredManNr = CredManr.SerNr;
    RecordInsert(MRr,true);
  end;

  return;
end;

global
procedure CredManCalcLastMonthPrinciple(var record CredManVc CredManr)
begin
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  LongInt i,rwcnt;
  record SMVc SMr;
  row SMVc SMrw;  
  record CredManSetBlock CredManSetb;

  if (CredManr.MaxMonthlyPayment>0) then begin
    BlockLoad(CredManSetb);
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
    BuildCredManInfoMatrix(CredManr,CredManPayPlanr,CredManSetb,SMr);
    rwcnt = MatRowCnt(SMr);
    MatRowGet(SMr,rwcnt-1,SMrw);
    CredManr.LastMonthPrinc = SMrw.DebVal;
  end else begin
    CredManr.LastMonthPrinc = blankval;
  end;
  return;
end;

global
procedure CredManCalcMaxMonthlyPayment(var record CredManVc CredManr)
begin
  
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  LongInt i,rwcnt;
  record SMVc SMr;
  row SMVc SMrw;  
  record CredManSetBlock CredManSetb;
  val origsum,tot,calcbal,int,totdeb,tmp;
  Integer origmonths;
  Integer months,monthtype;

  if (CredManr.LastMonthPrinc>0) then begin
/*
    BlockLoad(CredManSetb);
    origsum = CredManr.InvSum4;
    origmonths = CredManr.InstalmentMonths;
    monthtype = 1;
    if (CredManr.ChargeMethod==1) then begin
      monthtype = 2;
    end;

    CredManr.MaxMonthlyPayment = 0;
    CredManr.InvSum4 = CredManr.InvSum4 - CredManr.LastMonthPrinc;
    CredManr.InstalmentMonths = CredManr.InstalmentMonths - 1;
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
    BuildCredManInfoMatrix(CredManr,CredManPayPlanr,CredManSetb,SMr);
    rwcnt = MatRowCnt(SMr);
    calcbal = origsum;
    MatRowGet(SMr,rwcnt-1,SMrw);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(SMr,i,SMrw);
      if (SMrw.DebVal>0) then begin
        tot = tot + SMrw.DebVal;
        int = int + GetRateAmount(CredManr,CredManr.IntRate,monthtype,"","",calcbal,false,false);
        calcbal = calcbal - SMrw.DebVal;
        months = months + 1;
      end;
    end;

    tot = tot + int;

  //  CredManr.MaxMonthlyPayment = SMrw.DebVal + SMrw.CredVal;

//stupid workaround, but I can't figure out a better way

    tmp = tot/months;
    CredManr.InstalmentMonths = origmonths;
    CredManr.InvSum4 = origsum;
    CredManr.MaxMonthlyPayment = tmp;
    RecordClear(CredManPayPlanr);
    RecordClear(SMr);
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
    BuildCredManInfoMatrix(CredManr,CredManPayPlanr,CredManSetb,SMr);
    rwcnt = MatRowCnt(SMr);
    MatRowGet(SMr,rwcnt-1,SMrw);
    if (SMrw.DebVal>CredManr.LastMonthPrinc) then begin
      tmp = tmp+((SMrw.DebVal-CredManr.LastMonthPrinc)/months);
    end;
    CredManr.MaxMonthlyPayment = tmp;

    RecordClear(CredManPayPlanr);
    RecordClear(SMr);
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
    BuildCredManInfoMatrix(CredManr,CredManPayPlanr,CredManSetb,SMr);
    rwcnt = MatRowCnt(SMr);
    MatRowGet(SMr,rwcnt-1,SMrw);
    CredManr.LastMonthPrinc = SMrw.DebVal;
*/
    CredManr.MaxMonthlyPayment = blankval;
  end else begin
    CredManr.MaxMonthlyPayment = blankval;
  end;
  return;
end;

global
procedure InsertCredManDocSignerName(var record CredManVc CredManr,Integer num)
begin
  Boolean res;
  record CUVc CUr;
  record ContactRelVc CRr;
  
  CRr.ContactName = GetFieldValueByName(CredManr,"Surety" & num & "Signer",-1);
  CRr.CustCode = GetFieldValueByName(CredManr,"SuretyProvider" & num,-1);
  /*
  if (ReadFirstKey("Contact",CRr,2,true)) then begin
    CUr.Code = CRr.ContactCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      SetFieldValueByName(CredManr,"Surety" & num & "Position",CUr.JobDesc,-1);
    end;
  end;
  */
  return;
end;

global
procedure InsertBOESuretySignerName(var record BOESuretyVc BSr,Integer num)
begin
  Boolean res;
  record CUVc CUr;
  record ContactRelVc CRr;
  
  CRr.ContactName = GetFieldValueByName(BSr,"Surety" & num & "Signer",-1);
  CRr.CustCode = GetFieldValueByName(BSr,"SuretyProvider" & num,-1);
  if (ReadFirstKey("Contact",CRr,2,true)) then begin
    CUr.Code = CRr.ContactCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      SetFieldValueByName(BSr,"Surety" & num & "Position",CUr.JobDesc,-1);
    end;
  end;

  return;
end;

global
function Boolean HasCredManChanges(record CredManVc CredManr)
begin
  record CredManChangeVc Changer;
  Boolean res;

  Changer.CredManNr = CredManr.SerNr;
  if (ReadFirstKey("CredManDate",Changer,1,true)) then begin
    res = true;
  end;

  HasCredManChanges = res;
  return;
end;

procedure RemoveHiddenLine(var record IVVc IVr)
begin
  Integer i,rwcnt;
  row IVVc IVrw;

  rwcnt = MatRowCnt(IVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVr,i,IVrw);
    if (IVrw.stp==kInvoiceRowTypeHidden) then begin
      MatRowDelete(IVr,i);
      i = rwcnt;
    end;
  end;
  return;
end;

global
updating procedure TerminateCredMan(var record CredManVc CredManr,val prc,string custcode,Date selldate,Boolean sellf)
begin
  val calcbal,balance,cumint;
  Date lastinv,td;
  Integer i,rwcnt;
  record IVVc IVr,oldIVr;
  record CredManSetBlock CMb;
  row CredManPayPlanVc CredManPayPlanrw;
  record CredManPayPlanVc CredManPayPlan2r;
  record CredManPayPlanVc CredManPayPlanr;
  record CMInvoicingBlock CIb;
  record CredManVc tCredManr;
  val credval;
  string 255 spec;
  vector val vCredVal;
  array string 255 aCredVal;
  Date sd;
  Integer latedays;
  val lateamount,void;
  array string 255 lateformula;
  Boolean updf;
  Date calcsd;
  transaction Boolean gCreditSellerf;
  transaction string 255 gCreditSellerAccount;
  Date tmpdate;

  BlockLoad(CMb);
  BlockLoad(CIb);

  latedays = 0;
  lateamount = 0;
  sd = GetLastInvoiceDate(CredManr,selldate);
  GetCredManOverdueInvoices(CredManr,CMb,selldate,sd,latedays,lateamount,lateformula,true,false,calcsd);
  if (latedays>0 or lateamount>0) then begin
    //Setup always new invoice
    SetupLoanInvoice(CredManr,CMb,IVr,selldate,calcsd,selldate,true,false,"",2,true);
    AddLateFees(CredManr,CMb,IVr,latedays,lateamount,lateformula);
    SetupOverdueLangCode(IVr,CredManr);
    StoreLoanInvoice(IVr,CredManr);
  end;
  RecordClear(IVr);
  CreateSingleAgreementInvoice2(CredManr,selldate,IVr,"",false,true);

  //recalculate schedule if there is one
  //unok the invoice
  RecordCopy(tCredManr,CredManr);
  tCredManr.CancelDate = "";
  BuildCredManPayPlanCust(tCredManr,CredManPayPlanr,CMb,true,false);
  GetBuyOutData(tCredManr,CredManPayPlanr,CMb,tmpdate,balance,void);

  if (balance>0) then begin
    if (IVr.SerNr>-1) then begin
      RecordCopy(oldIVr,IVr);
      IVr.OKFlag = 0;
      if (RecordUpdate(oldIVr,IVr,true)==0) then begin
        updf = true;
      end;
    end else begin
      SetupLoanInvoice(CredManr,CMb,IVr,selldate,"","",true,false,"",1,true);
    end;
    AddInstalmentFixed(CredManr,CredManPayPlan2r,CMb,balance,selldate,cumint,calcbal,selldate,selldate,void);
    rwcnt = MatRowCnt(CredManPayPlan2r);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(CredManPayPlan2r,i,CredManPayPlanrw);
      SetupInvoiceRowAndAdd(CredManr,IVr,CredManPayPlanrw.ArtCode,CredManPayPlanrw.Spec,1,CredManPayPlanrw.Sum,CredManPayPlanrw.Reb,CredManPayPlanrw.PlanType,CredManPayPlanrw.FactoringInvoice);
    end;
    MergeItemsInInvoice(IVr,CredManr);
    RemoveHiddenLine(IVr);
    if (updf) then begin
      IVr.OKFlag = 1;
      RecordUpdate(oldIVr,IVr,true);
    end else begin
      StoreLoanInvoice(IVr,CredManr);
    end;
  end;

  if (sellf) then begin
    if (nonblank(CIb.SellingSalesAccount)) then begin
      gCreditSellerAccount = CIb.SellingSalesAccount;
      gCreditSellerf = true;
    end;
  end;

  CreditCredManInvoices(CredManr,prc,credval,vCredVal,sellf,selldate);
  ReadFirstMain(CredManr,1,true);

  if (nonblank(custcode) and credval>0) then begin
    RecordClear(IVr);
    RecordNew(IVr);
    RecordCopy(tCredManr,CredManr);
    tCredManr.CustCode = custcode;
    SetupLoanInvoice(tCredManr,CMb,IVr,selldate,"","",true,false,"",1,true);
    GetVectorTags(vCredVal,aCredVal);
    for (i=0;i<aCredVal.length;i=i+1) begin
      GetItemName(aCredVal[i],spec);
      SetupInvoiceRowAndAdd(CredManr,IVr,aCredVal[i],spec,1,vCredVal[aCredVal[i]] * prc / 100,blankval,17,-1);
    end;
    if (nonblank(CIb.SellingItem) and prc<100) then begin
      GetItemName(CIb.SellingItem,spec);
      SetupInvoiceRowAndAdd(CredManr,IVr,CIb.SellingItem,spec,1,credval * (100-prc) / 100,blankval,17,-1);
    end;

    StoreLoanInvoice(IVr,CredManr);
  end;

  if (gCreditSellerf) then begin
    gCreditSellerAccount = "";
    gCreditSellerf = false;
  end;


  return;
end;

global
procedure TerminateCredManFinish(var record CredManVc CredManr)
begin
  record CMOrderClassBlock OCb;
   
  BlockLoad(OCb);
  CredManr.OrderClass = OCb.TerminatedOrderClass;
  CredManr.Status = 5;

  return;
end;

global
updating procedure PrintBuyOutSummary(record CredManVc CredManr,record RcVc RepSpec,var string err,area farea,var record MailVc Mailr)
begin
  record LTxtVc LTxtr;
  row MailVc Mailrw;
  record CUVc CUr;
  record CMApplicationSetBlock CMb;
  record RLinkVc RLr;
  record Attach2Vc Attachr;
  string 255 mailboxnr,mailboxname;
  record MailSettingsBlock MSb;
  record RcVc tRepSpec;
  Longint pos,i,l;
  string 255 tstr,fname;
  area a;

  BlockLoad(CMb);
  LTxtr.Code = CMb.BuyoutText;
  if (ReadFirstMain(LTxtr,1,true)) then begin
    RecordNew(Mailr);
    Mailr.Header = LTxtr.Comment;    
    FindUserMailboxName(CurrentUser,mailboxnr,mailboxname);
    if (nonblank(mailboxname)) then begin
      ClearRow(Mailr,Mailrw,1);
      Mailrw.RowTyp = kMailRowTypeFrom;
      Mailrw.AddrCode = mailboxname;
      mailrw.Mailbox = StringToLongInt(mailboxnr);
      MatRowPut(Mailr,0,Mailrw);
    end else begin
      BlockLoad(MSb);
      ClearRow(Mailr,Mailrw,0);
      Mailrw.RowTyp = 1;
      Mailrw.AddrCode = MSb.FromSystem;
      MatRowPut(Mailr,0,Mailrw);
    end;
    
    CUr.Code = CredManr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      ClearRow(Mailr,Mailrw,1);
      Mailrw.RowTyp = kMailRowTypeTo;
      Mailrw.AddrCode = CUr.eMail;
      MatRowPut(Mailr,1,Mailrw);
    end;
    tRepSpec.f1 = CredManr.CustCode;

    tRepSpec.d1  = RepSpec.d1;
    tRepSpec.f13 = RepSpec.f13;
    tRepSpec.f16 = RepSpec.f16;
    tRepSpec.f17 = RepSpec.f17;
    tRepSpec.f18 = RepSpec.f18;
    tRepSpec.f19 = RepSpec.f19;
    tRepSpec.f12 = RepSpec.f12;
    tRepSpec.vals2 = RepSpec.vals2;


//    tRepSpec.f8 = GetCredManNr(CredManr.SerNr);
    if (LTxtr.HtmlFlag==0) then begin
      ParseTextField(a,LTxtr,tRepSpec,"LtxtVcReplaceToken");
      l = GetAreaLength(a);
      pos = 0;
      while (pos<l) begin
        tstr = GetStringFromArea(a,pos,255);
        AddToText(tstr,Mailr); 
        pos = pos+255;
      end;
    end else begin
      Mailr.HtmlFlag = 1;
    end;

    if (CheckAttachedFilesSizeToLetter(LTxtr)) then begin
      if (RecordInsert(Mailr,true)) then begin
        if (ReadRecordLink(Mailr,1,Attachr,RLr)) begin
          RecordRemove(RLr);
        end; 
        CopyRecordLinks(LTxtr,Mailr);
        if (Mailr.HtmlFlag==1) then begin
          ParseHtmlField(Mailr,tRepSpec,"MailVcReplaceToken");
        end;
        fname = "tmp/BuyoutSummary.pdf";
        WriteAreaToFile(farea,fname,0);
        RecordLinkFile(fname,0,Mailr,CurrentCompany);
        Delete_File(fname);
      end;
    end;
  end;

  return;
end;

function Boolean CanCreateGracePeriod(record CredManVc CredManr,Date sd,Date ed,Longint days,var string errstr)
begin
  Boolean res,TrHs;
  record CredManChangeVc Changer;

  res = true;

  if (days<0) then begin
    res = false;
    errstr = USetStr(200302);
  end;


  TrHs = true;
  Changer.CredManNr = CredManr.SerNr;
  while (LoopKey("CredManDate",Changer,1,TrHs)) begin
    if (Changer.CredManNr!=CredManr.SerNr) then begin
      TrHs = false;
    end else begin
      if (Changer.OKFlag==1 and Changer.LateFeeDays>-1) then begin
        if (DateInRange(sd,Changer.TransDate,Changer.EndDate)) then begin
          res = false;
        end;
        if (DateInRange(ed,Changer.TransDate,Changer.EndDate)) then begin
          res = false;
        end;
        if (res==false) then begin
          TrHs = false;
          errstr = USetStr(200303);
        end;
      end;
    end;
  end;

  CanCreateGracePeriod = res;
  return;
end;

global
updating procedure CredManGracePeriodRemote(record RcVc RepSpec,record CredManVc CredManr,var string errstr)
begin
  record CredManChangeVc Changer;

  if (CanCreateGracePeriod(CredManr,RepSpec.d1,RepSpec.d2,RepSpec.long1,errstr)) then begin
    RecordNew(Changer);
    Changer.SerNr = NextSerNr("CredManChangeVc",RepSpec.d1,-1,false,"");
    Changer.TransDate = RepSpec.d1;
    Changer.EndDate = RepSpec.d2;
    Changer.LateFeeDays = RepSpec.long1;
    Changer.OKFlag = 1;
    Changer.CredManNr = CredManr.SerNr;
    RecordStore(Changer,true);
  end;

  return;
end;

global
function Boolean FindFirstFeeInvoice(record CredManVc CredManr,var record IVVc IVr)
begin
  record CredHistVc CredHistr;
  Boolean TrHs,res;
  record ARVc ARr;

  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.TransDate = CredManr.startDate;
  while (LoopKey("CredManTypeDate",CredHistr,2,TrHs)) begin
    if (CredHistr.TransDate>=CredManr.FirstInvDate) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==0) then begin
        IVr.SerNr = CredHistr.RecordNr;
        if (ReadFirstMain(IVr,1,true)) then begin
          ARr.InvoiceNr = IVr.SerNr;
          if (ReadFirstMain(ARr,1,true)) then begin
            if (ARr.RVal>0) then begin
              res = true;
              TrHs = false;
            end;
          end;
        end;
      end;
    end;
  end;

  FindFirstFeeInvoice = res;
  return;
end;

global 
function integer GenCredManObj(record CredManVc CMr, record CMApplicationSetBlock CMAb,var record ObjVc Objr)
begin
  record OTVc OTr;
  Integer err;
  
  OTr.Code = CMAb.AutoOTCode;
  ReadFirstMain(OTr,1,true);
  Objr.Code = OTr.Start & CMr.SerNr;
  if (ReadFirstMain(Objr,1,true)==false) then begin
    Objr.Code = OTr.Start & CMr.SerNr;
    if (nonblank(CMr.OfficialSerNr)) then begin
      Objr.Comment = CMr.Addr0 & " " & CMr.OfficialSerNr;
    end else begin
      Objr.Comment = CMr.Addr0 & " " & CMr.SerNr;
    end;
    Objr.OTCode = CMAb.AutoOTCode;
    if (OTr.Length>0) then begin
      if (len(Objr.Code)!=OTr.Length) then begin
        err = 20444;
      end;
    end;
    if (err!=0) then begin
      Objr.Code = "";
    end;
  end;
  GenCredManObj = err; 
  return;
end;
 
global
updating procedure CreateCredManObject(var record CredManVc CMr,Boolean autof,Boolean showerrors)
begin
  record CMApplicationSetBlock CMAb;
  record OTVc OTr;
  record ObjVc Objr;
  Boolean testf;
  Integer err;
  
  BlockLoad(CMAb);
  testf = false;
  if (autof) then begin
    if (CMAb.AutoCreateObject!=0) then begin testf = true; end;
  end else begin
    testf = true;
  end;
  if (testf) then begin
    err = GenCredManObj(CMr,CMAb,Objr);
    if (err!=0) then begin
      if (showerrors) then begin
        MessageBox(err,"");
      end;
      Objr.Code = "";
    end else begin
      RecordStore(Objr,false);
    end;
    CMr.Objects = AddObjectToObjectList(CMr.Objects,Objr.Code);
  end;
  return;
end;


global updating function boolean RA_CreateCredManOfferEmail(record RcVc RepSpec, record CredManVc CMr, var record MailVc Mailr)
begin
  record CMApplicationSetBlock CMb;
  boolean res;
  record CUVc CUr;
  string 255 email;
  row MailVc Mailrw;
  string 255 tstr;
  LongInt pos,l;
  Integer i,emcnt,cscnt;
  area a;
  record LTxtVc LTxtr;
  string 255 mailboxnr,mailboxname;
  record IVMailBlock IVMailr;
  record CredManVc CredManr;
  record CredManSetBlock CredManSetb;
  record CredManPayPlanVc CredManPayPlanr;
  LongInt rwcnt;
  record SMVc SMr;
  row SMVc SMrw;
  val tot,interest,fees;
  
  BlockLoad(CMb);
  if (nonblank(CMb.EmailOfferText)) then begin 
    CUr.Code = CMr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      email = CredManCustEmail(CUr);
    end;
    
    
    
    LTxtr.Code = CMb.EmailOfferText;
    if (ReadFirstMain(LTxtr,1,true)) then begin
      RecordNew(Mailr);
      Mailr.Header = LTxtr.Comment;    
      if (nonblank(mailboxname)) then begin
        ClearRow(Mailr,Mailrw,1);
        Mailrw.RowTyp = kMailRowTypeFrom;
        Mailrw.AddrCode = mailboxname;
        mailrw.Mailbox = StringToLongInt(mailboxnr);
        MatRowPut(Mailr,0,Mailrw);
      end else begin
        ClearRow(Mailr,Mailrw,0);
        Mailrw.RowTyp = 1;
        BlockLoad(IVMailr);
        Mailrw.AddrCode = IVMailr.EmailSender;
        MatRowPut(Mailr,0,Mailrw);
      end;
      ClearRow(Mailr,Mailrw,1);
      Mailrw.RowTyp = kMailRowTypeTo;
      Mailrw.AddrCode = email;
      MatRowPut(Mailr,1,Mailrw);
      
      //CredManr.SerNr = -1;
      if(nonblank(RepSpec.d1))then begin
        CredManr.TransDate = RepSpec.d1;
        CredManr.startDate = RepSpec.d1;
      end;
      
      /*
      d1,false,PasteCurDate);
  EditFieldTL(h,v+=vs,120,"Pause Months",M4Qty,Normal,0,vals0,false,0);
  EditFieldTL(h,v+=vs,120,"Prolong Months",M4Qty,Normal,0,vals1,false,0);
  EditFieldTL(h,v+=vs,120,"New Rate",M4Rate,Normal,0,vals3,false,0);
  EditFieldTL(h,v+=vs,120,"Extra Value",M4Rate,Normal,0,vals4,false,0);
  EditField(h,v+=vs,100,"Change Purpose",Normal,f1,false,AgrChangesPurpSClass);
      
      */
      
      
        BlockLoad(CredManSetb);
        BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
        BuildCredManInfoMatrix(CredManr,CredManPayPlanr,CredManSetb,SMr);
        rwcnt = MatRowCnt(SMr);
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(SMr,i,SMrw);
          fees = fees + SMrw.DebVal2;
          interest = interest + SMrw.CredVal;
          tot = tot + SMrw.CurDebVal;
        end;
        RepSpec.f3 = CredManr.CurncyCode;
        RepSpec.vals0 = fees;
        RepSpec.vals1 = interest;
        RepSpec.vals2 = tot;
      if (LTxtr.HtmlFlag==0) then begin
        ParseTextField(a,LTxtr,RepSpec,"LtxtVcReplaceToken");
        l = GetAreaLength(a);
        pos = 0;
        while (pos<l) begin
          tstr = GetStringFromArea(a,pos,255);
          AddToText(tstr,Mailr); 
          pos = pos+255;
        end;
      end else begin
        Mailr.HtmlFlag = 1;
      end;

      if (CheckAttachedFilesSizeToLetter(LTxtr)) then begin
        if (qupdating.StoreEmailRecord(LTxtr,Mailr,RepSpec)) then begin
          res = true;
          createrecordlink(Mailr,currentcompany,CredManr,currentcompany);
          createrecordlink(CredManr,currentcompany,Mailr,currentcompany);
        end;
      end;
    end;
  
  end;
  
  RA_CreateCredManOfferEmail = res;
return;
end;