//<halrule>server-only</halrule>
external inner function string 255 StrReplace(string,string,string);

global
function Boolean GetConditionTextRecord(Longint sernr,Integer type,string id,var record ConditionTextVc tCTr)
begin
  record ConditionTextVc CTr;
  record DefConditionTextVc PCTr;
  Integer i,lcnt;
  string 255 code;
  Boolean res;

  CTr.CredManNr = sernr;
  CTr.Type = type;
  CTr.ID = id;
  if (ReadFirstMain(CTr,3,true)) then begin
    res = true;
    RecordCopy(tCTr,CTr);
  end;

  GetConditionTextRecord = res;
  return;
end;

global
updating function Boolean GetConditionText(Longint sernr,Integer type,string id,var record ConditionTextVc tCTr)
begin
  record ConditionTextVc CTr;
  record DefConditionTextVc PCTr;
  Integer i,lcnt;
  string 255 code;

  CTr.CredManNr = sernr;
  CTr.Type = type;
  CTr.ID = id;
  if (ReadFirstMain(CTr,3,true)==false) then begin
    RecordNew(CTr);
    CTr.CredManNr = sernr;
    CTr.Type = type;
    CTr.ID = id;
    RecordInsert(CTr,true);
  end;

  if (SizeTextCnt(CTr)==0) then begin
    if (type==1) then begin//payment conditions
      code = "PMT";
    end else begin
      code = "OTHER";
    end;
    PCTr.Code = code;
    if (ReadFirstMain(PCTr,1,true)) then begin
      lcnt = LineTextCnt2(PCTr);
      for (i=0;i<lcnt;i=i+1) begin
        LineTextPut2(CTr,i,LineTextGet2(PCTr,i));
      end;
    end;
    RecordInsert(CTr,true);
  end; 
  RecordCopy(tCTr,CTr);

  GetConditionText = true;
  return;
end;


function string 255 GetContractFeeInvoice(record CredManVc CredManr)
begin
  Boolean TrHs;
  record CredHistVc CredHistr;
  record IVVc IVr;
  string 255 res;
  
  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.TransDate = AddDay(CredManr.startDate,-5);
  while (LoopKey("CredManTypeDate",CredHistr,2,TrHs)) begin
    if (CredHistr.CredManNr!=CredManr.SerNr or CredHistr.TransDate>=CredManr.FirstInvDate) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==0) then begin
        if (CredHistr.RecordType==4 and CredHistr.TransDate<CredManr.FirstInvDate) then begin
          IVr.SerNr = CredHistr.RecordNr;
          if (ReadFirstMain(IVr,1,true)) then begin
            if (nonblank(IVr.OfficialSerNr)) then begin
              res = IVr.OfficialSerNr;
            end else begin
              res = IVr.SerNr;
            end;
            TrHs = false;
          end;
        end;
      end;
    end;  
  end;

  GetContractFeeInvoice = res;
  return;
end;

global
function string 255 GetConditionsText2(record CredManVc CredManr,Integer type,string id)
begin
  record ConditionTextVc CTr;
  Integer j;
  string 255 res,feeivno;
  
  if (GetConditionTextRecord(CredManr.SerNr,type,id,CTr)) then begin
    res = "";
    for (j=0;j<LineTextCnt2(CTr);j=j+1) begin
      res = res & LineTextGet2(CTr,j);
    end;

    res = StrReplace(res,chr(13),"\\n");
    feeivno = GetContractFeeInvoice(CredManr);
    res = StrReplace(res,"?feeinvoice?",feeivno);
  end;

  GetConditionsText2 = res;
  return;
end;

global
function string 255 GetConditionsText(record CredManVc CredManr,Integer type)
begin
  GetConditionsText = GetConditionsText2(CredManr,type,"");
  return;
end;
