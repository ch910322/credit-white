external function string 255 GetCredManNr(Longint);
external function val GetFutureCredManBalance(record CredManVc,record CredManSetBlock,Date);
external function Boolean BuildCredManInfoMatrixOnSchedule(record CredManVc,record CredManSetBlock,var record SMVc);
external procedure BuildCredManPayPlanCust(record CredManVc,var record CredManPayPlanVc,record CredManSetBlock,Boolean,Boolean);
external procedure BuildCredManInfoMatrix(record CredManVc,record CredManPayPlanVc,record CredManSetBlock,var record SMVc);
external inner function Integer GetCheck(string,string,LongInt,var string,var string,var string,var val,var Date,var Date);
external inner function LongInt DateDiff(Date,Date);
external inner function Boolean SetInSet2(string,string);

function string 255 GetCompanyType(record CUVc CUr,record InvegaReportBlock IRb)
begin
  record CClassVc CClassr;
  Longint pos;
  string 255 cls,res;
  
  if (nonblank(IRb.CompTypeClass)) then begin
    while (GetNextSubstring(CUr.Classification,pos,",",cls)) begin
      CClassr.Code = cls;
      if (ReadFirstMain(CClassr,1,true)) then begin
        if (CClassr.CType==IRb.CompTypeClass) then begin
          res = CClassr.Comment;
          goto LGetCompanyType;
        end;
      end;
    end;
  end;

LGetCompanyType:;
  GetCompanyType = res;
  return;
end;

function string 255 GetCompanyEVKRCode(record CUVc CUr,record InvegaReportBlock IRb)
begin
  record CClassVc CClassr;
  Longint pos;
  string 255 cls,res;
  
  if (nonblank(IRb.EVKRClassType)) then begin
    while (GetNextSubstring(CUr.Classification,pos,",",cls)) begin
      CClassr.Code = cls;
      if (ReadFirstMain(CClassr,1,true)) then begin
        if (CClassr.CType==IRb.EVKRClassType) then begin
          res = CClassr.Comment;
          goto LGetCompanyEVKRCode;
        end;
      end;
    end;
  end;

LGetCompanyEVKRCode:;
  GetCompanyEVKRCode = res;
  return;
end;

function val FindInvoicePaidAmount_OP(LongInt VISerNr,LongInt TransNr)
begin
  val res;
  record OPVc OPr;
  row OPVc OPrw;
  Integer i,rwcnt;
  Integer chktyp;
  string 255 tstr;
  Date td,regdate;
  val chkamount;

  OPr.SerNr = TransNr;
  if (ReadFirstMain(OPr,1,true)) then begin
    rwcnt = MatRowCnt(OPr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(OPr,i,OPrw);
      if (OPrw.VISerNr==VISerNr) then begin
        chktyp = GetCheck(OPr.PayMode,OPrw.PayMode,OPrw.ChequeNr,tstr,tstr,tstr,chkamount,td,regdate);
        switch (chktyp) begin
          case 0:
            res = res + OPrw.PInvVal;
          case 2:
            res = res + chkamount;
        end;
      end;
    end;
  end;
  FindInvoicePaidAmount_OP = res;
  return;
end;

function string 255 GetCredManTransactionList(record CredManVc CredManr,Date sd,Date ed,var val paid,var val princ,var val interest,var val overdues)
begin
  string 255 res;
  record CredHistVc CredHistr,tCredHistr;
  Boolean TrHs;
  vector string 255 vDates;
  record OPrsVc OPrsr;
  array string 255 aDates;
  Integer i;

  paid = 0;
  princ = 0;
  interest = 0;
  overdues = 0;
  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.TransDate = sd;
  while (LoopKey("CredManTypeDate",CredHistr,2,TrHs)) begin
    if (CredHistr.CredManNr!=CredManr.SerNr or CredHistr.TransDate>ed) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==1 or CredHistr.RecordType==2) then begin
        vDates[CredHistr.TransDate] = true;
        tCredHistr.SerNr = CredHistr.PreviousNr;
        if (ReadFirstMain(tCredHistr,1,true)) then begin
          switch (tCredHistr.ChargeType) begin
            case 5:
              princ = princ + CredHistr.Sum;
            case 6:
              interest = interest + CredHistr.Sum;
            case 9:
              overdues = overdues + CredHistr.Sum;
          end;
        end;
      end;
    end;
  end;

  if (CredManr.VINr>0) then begin
    OPrsr.TransType = kOPrsTransTypePayment;
    OPrsr.TransDate = sd;
    OPrsr.VINr = CredManr.VINr;
    TrHs = true;
    while (LoopKey("VIKey",OPrsr,3,TrHs)) begin
      if (OPrsr.TransDate>ed or OPrsr.VINr!=CredManr.VINr or OPrsr.TransType!=kOPrsTransTypePayment) then begin
        TrHs = false;
      end else begin
        vDates[OPrsr.TransDate] = true;
        paid = paid + FindInvoicePaidAmount_OP(OPrsr.VINr,OPrsr.TransNr);
      end;
    end;
  end;

  GetVectorTags(vDates,aDates);
  for (i=0;i<aDates.length;i=i+1) begin
    if (i>0) then begin
      res = res & ", ";
    end;
    res = res & aDates[i];
  end;

  GetCredManTransactionList = res;
  return;
end;

function Integer GetLateDaysInPeriod(record CredHistVc CredHistr,Date sd,Date ed,var val dueamount)
begin
  Boolean TrHs;
  record CredHistVc CredPayr;
  val paysum;
  Integer res;

  paysum = CredHistr.Sum;
  TrHs = true;
  CredPayr.PreviousNr = CredHistr.SerNr;
  while (LoopKey("PreviousNr",CredPayr,1,TrHs)) begin
    if (CredPayr.PreviousNr!=CredHistr.SerNr) then begin
      TrHs = false;
    end else begin
      if (CredPayr.TransDate<=ed) then begin
        paysum = paysum - CredPayr.Sum;
      end;
    end;
  end;

  if (paysum>0) then begin
    res = DateDiff(ed,CredHistr.PayDate)+1;
  end;
  dueamount = paysum;

  GetLateDaysInPeriod = res;
  return;
end;

function Integer MAX(Longint v1,Longint v2)
begin
  Longint res;

  if (v1>v2) then begin
    res = v1;
  end else begin
    res = v2;
  end;

  MAX = res;
  return;
end;

procedure GetCredManLateAmounts(record CredManVc CredManr,Date sd,Date ed,Integer chargetype,var Longint maxlatedays,var val dueamount)
begin
  string 255 res;
  record CredHistVc CredHistr;
  Boolean TrHs;
  Longint latedays;

  dueamount = 0;
  maxlatedays = 0;
  TrHs = true;
  CredHistr.CredManNr = CredManr.SerNr;
  CredHistr.ChargeType = chargetype;
  while (LoopKey("CredManChargeType",CredHistr,2,TrHs)) begin
    if (CredHistr.CredManNr!=CredManr.SerNr or CredHistr.ChargeType!=chargetype) then begin
      TrHs = false;
    end else begin
      if (CredHistr.RecordType==0 and DateInRange(CredHistr.TransDate,sd,ed)) then begin
        latedays = GetLateDaysInPeriod(CredHistr,sd,ed,dueamount);
        maxlatedays = MAX(maxlatedays,latedays);
      end;
    end;
  end;

  return;
end;

global
procedure InvegaFundRn(record RcVc RepSpec)
begin
  record InvegaReportBlock IRb;
  record CredManVc CredManr;
  record CUVc CUr;
  Boolean TrHs,testf;
  record CredManSetBlock CredManSetb;
  array Integer p;
  Integer i,num;
  Longint principlelatedays,interestlatedays,overdueslatedays;
  val principlelateamount,interestlateamount,overdueslateamount;
  val princ,interest,overdues,startbal,endbal,paidval,rate;
  string 255 translist,overduesign;
  Longint cnt;

  StartReportJob(UsetStr(200266));

  EndHeader;
  num = 0;
  for (i=0;i<26;i=i+1) begin
    p[i] = num;
    num = num + 50;
  end;
  cnt = 1;

  StartFormat(15);
  OutString(0,0,1,false);
  OutString(p[1],0,2,false);
  OutString(p[2],0,3,false);
  OutString(p[3],0,4,false);
  OutString(p[4],0,5,false);
  OutString(p[5],0,6,false);
  OutString(p[6],0,7,false);
  OutString(p[7],0,8,false);
  OutString(p[8],0,9,false);
  OutString(p[9],0,10,false);
  OutString(p[10],0,11,false);
  OutString(p[11],0,12,false);
  OutString(p[12],0,13,false);
  OutString(p[13],0,14,false);
  OutString(p[14],0,15,false);
  OutString(p[15],0,16,false);
  OutString(p[16],0,17,false);
  OutString(p[17],0,18,false);
  OutString(p[18],0,19,false);
  OutString(p[19],0,20,false);
  OutString(p[20],0,21,false);
  OutString(p[21],0,22,false);
  OutString(p[22],0,23,false);
  OutString(p[23],0,24,false);
  OutString(p[24],0,25,false);
  OutString(p[25],0,26,false);

  EndFormat;


  BlockLoad(IRb);
  BlockLoad(CredManSetb);
  while (LoopMain(CredManr,1,true)) begin
    testf = true;
    if (nonblank(RepSpec.f1) and SetInSet(CredManr.Type,RepSpec.f1)==false) then begin
      testf = false;
    end;
    if (nonblank(RepSpec.f2) and RepSpec.f2!=CredManr.CustCode) then begin
      testf = false;
    end;
    if (nonblank(RepSpec.f3) and SetInSet2(RepSpec.f3,CredManr.Classifications)) then begin
      testf = false;
    end;
    if (nonblank(RepSpec.f4) and RepSpec.f4!=CredManr.SalesMan) then begin
      testf = false;
    end;
    if (testf) then begin
      CUr.Code = CredManr.CustCode;
      rate = CredManr.IntRate;
      if (CredManr.AnnualRate==0) then begin
        rate = rate * 12;
      end;
      startbal = GetFutureCredManBalance(CredManr,CredManSetb,RepSpec.sStartDate);
      endbal = GetFutureCredManBalance(CredManr,CredManSetb,RepSpec.sEndDate);
      translist = GetCredManTransactionList(CredManr,RepSpec.sStartDate,RepSpec.sEndDate,paidval,princ,interest,overdues);
      GetCredManLateAmounts(CredManr,RepSpec.sStartDate,RepSpec.sEndDate,5,principlelatedays,principlelateamount);
      GetCredManLateAmounts(CredManr,RepSpec.sStartDate,RepSpec.sEndDate,6,interestlatedays,interestlateamount);
      GetCredManLateAmounts(CredManr,RepSpec.sStartDate,RepSpec.sEndDate,9,overdueslatedays,overdueslateamount);
      
      overduesign = "Ne";
      if (principlelatedays>90 or interestlatedays>90 or overdueslatedays>90 or CUr.OnHoldFlag==1) then begin
        overduesign = "Taip";
      end;

      ReadFirstMain(CUr,1,true);
      StartFormat(15);
      OutString(0,0,cnt,false);
      OutString(p[1],0,CredManr.Addr0,false);
      OutString(p[2],0,CUr.RegNr1,false);
      OutString(p[3],0,CUr.eMail,false);
      OutString(p[4],0,"\"" & CUr.Phone & "\"",false);
      OutString(p[5],0,GetCompanyType(CUr,IRb),false);
      OutString(p[6],0,GetCompanyEVKRCode(CUr,IRb),false);
      OutString(p[7],0,CUr.InvAddr3,false);
      OutString(p[8],0,CredManr.TransDate,false);
      OutString(p[9],0,GetCredManNr(CredManr.SerNr),false);
      OutString(p[10],0,CredManr.InvSum4,false);
      OutString(p[11],0,rate,false);
      OutString(p[12],0,CredManr.endDate,false);
      OutString(p[13],0,startbal,false);
      OutString(p[14],0,translist,false);
      OutString(p[15],0,paidval,false);
      OutString(p[16],0,princ,false);
      OutString(p[17],0,interest,false);
      OutString(p[18],0,overdues,false);
      OutString(p[19],0,endbal,false);
      OutString(p[20],0,principlelatedays,false);
      OutString(p[21],0,interestlatedays,false);
      OutString(p[22],0,interestlateamount,false);
      OutString(p[23],0,overdueslateamount,false);
      OutString(p[24],0,"",false);
      OutString(p[25],0,overduesign,false);

      EndFormat;
      cnt = cnt + 1;
    end;  
  
  end;

  EndJob;

  return;
end;