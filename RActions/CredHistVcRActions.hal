//<halrule>server-only</halrule>
external  procedure CredManSetDatesCust(var record CredManVc);
external updating procedure RecreateAgreementSchedule(record CredManVc);
external procedure RecreateAgreementDocument(var record AgreementDocVc,Boolean);
external updating procedure GenerateAgreementSchedule(record CredManVc,Boolean);

global
function LongInt CredHistVcRecordInIndex(record CredHistVc CredHistr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (indexname=="CredManTransDate") then begin 
    res = 0; 
    if (CredHistr.MainRecord==1) then begin 
      res = 1;
    end;
  end;
  CredHistVcRecordInIndex = res;
  return;
end; 

updating procedure FinishProlongSession(Longint sernr)
begin
  record ProlongSessionVc PSr,oldPSr;
  record CredManVc CredManr,oldCredManr;
  record AgreementDocVc ADr;
  
  PSr.CredManNr = sernr;
  PSr.PaidFlag = 0;
  if (ReadFirstKey("CredManPaid",PSr,2,true)) then begin
    if (PSr.ProlongWhenPaid==1) then begin
      CredManr.SerNr = sernr;
      if (ReadFirstMain(CredManr,1,true)) then begin
        RecordCopy(oldCredManr,CredManr);
        CredManr.PauseDate = PSr.PauseDate;
        CredManr.ProlongDate = PSr.ProlongDate;
        CredManr.PauseMonths = PSr.PauseMonths;
        CredManr.ProlongMonths = PSr.ProlongMonths;
        CredManSetDatesCust(CredManr);
        if (RecordUpdate(oldCredManr,CredManr,true)==0) then begin
          if (PSr.RecalcSchedule == 1) then begin
            //RecreateAgreementSchedule(CredManr);
            ADr.RecordType = 0;
            ADr.RecordNr = CredManr.SerNr;
            ADr.Type = 2;
            if (ReadFirstKey("RecordNr",ADr,3,true)) then begin
              GenerateAgreementSchedule(CredManr,true);
              threadasync.RecreateAgreementDocument(ADr,true);
            end;
          end;
        end;
      end;
    end;
    RecordCopy(oldPSr,PSr);
    PSr.PaidFlag = 1;
    RecordUpdate(oldPSr,PSr,true);
  end;

  return;
end;

updating procedure UpdateInvoicedCredHist(Longint prevnr)
begin
  record CredHistVc CredHistr,pCredHistr,oldCredHistr,CredHist2r;
  Boolean TrHs;
  val sum,due;
  Integer pdf;
  record ARVc ARr;

  CredHistr.SerNr = prevnr;
  if (ReadFirstMain(CredHistr,1,true)) then begin
    CredHist2r.MainRecord = 1;
    CredHist2r.RecordNr = CredHistr.RecordNr;
    CredHist2r.RecordType = 0;
    if (ReadFirstKey("MainRecord",CredHist2r,3,true)) then begin
      ARr.InvoiceNr = CredHist2r.RecordNr;
      if (ReadFirstMain(ARr,1,true)) begin
        due = ARr.RVal;
      end;
      RecordCopy(oldCredHistr,CredHist2r);
      CredHist2r.PaidSum = CredHist2r.InvSum - ARr.RVal;
      if (ARr.RVal==0) then begin
        CredHist2r.PaidFlag = 1;
        if (CredHist2r.ChargeType==14) then begin//prolong session
          FinishProlongSession(CredHist2r.CredManNr);
        end;
      end else begin
        CredHist2r.PaidFlag = 0;
      end;
      RecordUpdate(oldCredHistr,CredHist2r,true);
    end;

  end;
/*
    TrHs = true;
    pCredHistr.PreviousNr = prevnr;
    while (LoopKey("PreviousNr",pCredHistr,1,TrHs)) begin
      if (pCredHistr.PreviousNr!=prevnr) then begin
        TrHs = false;
      end else begin
        sum = sum + pCredHistr.Sum;
      end;
    end;
    if (CredHistr.Sum==sum) then begin
      pdf = 1;
    end;
    RecordCopy(oldCredHistr,CredHistr);
    CredHistr.PaidFlag = pdf;
    CredHistr.PaidSum = sum;
    RecordUpdate(oldCredHistr,CredHistr,true);
*/


  return;
end;

global
updating function LongInt CredHistVcRecordSaveAfter(var record CredHistVc CredHistr,record CredHistVc CredHist2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  res = 0;

  //if this is payment then we have to check invoice records and if it is fully paid
  if (CredHistr.RecordType==1 or CredHistr.RecordType==2 or CredHistr.RecordType==3) then begin
    UpdateInvoicedCredHist(CredHistr.PreviousNr);
  end;

  CredHistVcRecordSaveAfter = res;
  return;
end;

global
updating function LongInt CredHistVcRecordRemoveAfter(var record CredHistVc CredHistr,record CredHistVc CredHist2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record ProlongSessionVc PSr;

  res = 0;

  //if this is payment then we have to update invoice history as well
  if (CredHistr.RecordType==1 or CredHistr.RecordType==2 or CredHistr.RecordType==3) then begin
    UpdateInvoicedCredHist(CredHistr.PreviousNr);
  end;
  if (CredHistr.ChargeType==14) then begin
    PSr.CredManNr = CredHistr.CredManNr;
    PSr.PaidFlag = 0;
    if (ReadFirstKey("CredManPaid",PSr,2,true)) then begin
      RecordRemove(PSr);
    end;
  end;

  CredHistVcRecordRemoveAfter = res;
  return;
end;