//<halrule>server-only</halrule>
external inner procedure CredManVc_PasteType(var record CredManVc);
external inner procedure CUCreditLimit(record CUVc,Date,string,var val,var LongInt);
external function val GetMonthlyPaymentAmount(record CredManVc,Integer);
external updating procedure ClearAgreementDocRecords(Longint,Integer);
external procedure RecalcCredManFees(var record CredManVc);
external function Boolean GetCredLegalNrLine(string,string,Date,string,string,var row CredLegalInvNrBlock,Integer);
external inner function string 50 NextLegalSerNr(string,LongInt,Date,string,string,string);
external updating procedure DeleteCredOffSerNr(LongInt,string);
external updating procedure StoreCredOffSerNrIV(LongInt,string,string);
external procedure BuildCredManPayPlanCust(record CredManVc,var record CredManPayPlanVc,record CredManSetBlock,Boolean,Boolean);
external inner function Boolean GetAccName(string,var string,Integer);

global
function LongInt CredManVcRecordDefaults(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record BaseCurBlock BCb;
  record CredManSetBlock CMb;
  record CMOrderClassBlock OCb;

  BlockLoad(BCb);
  BlockLoad(CMb);
  BlockLoad(OCb);
  CredManr.CurncyCode = BCb.BaseCur1;
  CredManr.MonthlyPaymentDay = CMb.InvDay;
  CredManr.FirstIntDate = CurrentDate;
  CredManr.Status = 0;

  res = inner.CredManVcRecordDefaults(CredManr,CredMan2r,stat,long4);
  CredManVc_PasteType(CredManr);
  RecalcCredManFees(CredManr);


  CredManr.OrderClass = OCb.CMDefOrderClass;

  CredManVcRecordDefaults = res;
  return;
END;

global
function LongInt CredManVcRecordDuplicate(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record BaseCurBlock BCb;
  record CredManSetBlock CMb;
  val t;

  BlockLoad(BCb);
  BlockLoad(CMb);
  CredManr.CurncyCode = BCb.BaseCur1;
  CredManr.MonthlyPaymentDay = CMb.InvDay;
  CredManr.StateChangeDate = CurrentDate;
  CredManr.StateChangeTime = CurrentTime;
  CredManr.StateNotified = 0;
  CredManr.VersionNo = -1;
  CredManr.OfficialSerNr = "";
  CredManr.MoneyTransferDate = "";
  CredManr.Status = 0;
  CredManr.VINr = -1;
  CredManr.Status = 0;
  CredManr.SignSurety = 0;
  CredManr.SignAgreement = 0;
  CredManr.SignVekselis = 0;
  CredManr.SignSchedule = 0;
  CredManr.Surety1Signed = 0;
  CredManr.Surety2Signed = 0;
  CredManr.Surety3Signed = 0;
  CredManr.Surety4Signed = 0;
  CredManr.Surety5Signed = 0;
  CredManr.Surety6Signed = 0;
  CredManr.BOE1Signed = 0;
  CredManr.BOE2Signed = 0;
  CredManr.BOE3Signed = 0;
  CredManr.BOE4Signed = 0;
/*
  CredManr.FactInvAmount = t;
  CredManr.LoanToValue = t;
  CredManr.FactBuyer = "";
  CredManr.FactBuyerName = "";
  CredManr.FactInvNumber = "";
  CredManr.FactComment = "";
  CredManr.FactInsured = 0;
*/

  res = inner.CredManVcRecordDuplicate(CredManr,CredMan2r,stat,long4);
  
  CredManVcRecordDuplicate = res;
  return;
END;

function val GetFactoringTotalAgreementWorth(string cucode,Date enddate,Longint sernr)
begin
  record CredManVc CredManr;
  Boolean TrHs;
  val res;

  TrHs = true;
  CredManr.FactBuyer = cucode;
  CredManr.endDate = CurrentDate;
  while (LoopKey("FactBuyer",CredManr,2,TrHs)) begin
    if (CredManr.FactBuyer!=cucode ) then begin//or CredManr.endDate>enddate?
      TrHs = false;
    end else begin
      if (CredManr.startDate<=CurrentDate and CredManr.InstalmentType==4) then begin
        if (CredManr.SerNr!=sernr) then begin
          res = res + CredManr.FactInvAmount;
        end;
      end;
    end;
  end;
  
  GetFactoringTotalAgreementWorth = res;
  return;
end;

global
updating function LongInt CredManVcRecordCheck(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record CredManVc tCredManr;
  Boolean TrHs,credf;
  Longint crlimitdays;
  val crlimit,totworth;
  record CUVc CUr;
  record CredManFactVc CMFr;
  row CredLegalInvNrBlock LegalInvNrrw;
  record CredManPayPlanVc CredManPayPlanr;
  row CredManPayPlanVc CredManPayPlanrw;
  LongInt i,rwcnt;
  record CredManSetBlock CredManSetb;

  res = 0;

  if (CredManr.InstalmentType==4) then begin
/*
    if (CredManr.FactInvAmount<=0) then begin
      RecordCheckError(1058,"",-1,"FactInvAmount");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
    if (blank(CredManr.FactInvNumber)) then begin
      RecordCheckError(1058,"",-1,"FactInvNumber");
      res = -1;
      goto LCredManVcRecordCheck;
    end else begin
      TrHs = true;
      tCredManr.FactInvNumber = CredManr.FactInvNumber;
      while (LoopKey("FactInvNumber",tCredManr,1,TrHs)) begin
        if (tCredManr.FactInvNumber!=CredManr.FactInvNumber) then begin
          TrHs = false;
        end else begin
          if (tCredManr.SerNr!=CredManr.SerNr) then begin
            RecordCheckError(200100,"",-1,"FactInvNumber");
            res = -1;
            goto LCredManVcRecordCheck;
          end;
        end;
      end;
    end;
  */
    CMFr.CredManNr = CredManr.SerNr;
    if (ReadFirstMain(CMFr,1,true)) then begin
      if (CMFr.Total>CredManr.InvSum4) then begin
        RecordCheckError(200101,"",-1,"InvSum4");
        res = -1;
        goto LCredManVcRecordCheck;
      end;
    end;
  end;

  if (CredManr.MonthlyPaymentDay<=0) then begin
    RecordCheckError(1058,"",-1,"MonthlyPaymentDay");
    res = -1;
    goto LCredManVcRecordCheck;
  end;

  if (CredManr.InstalmentType==6) then begin
    if (CredManr.PrincipleDelayMonths<=0) then begin
      RecordCheckError(1058,"",-1,"PrincipleDelayMonths");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
    if (CredManr.PrincipleDelayInterest<=0) then begin
      RecordCheckError(1058,"",-1,"PrincipleDelayInterest");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
  end;
  if (CredManr.MaxMonthlyPayment>0) then begin
    if (CredManr.MaxMonthlyPayment<GetMonthlyPaymentAmount(CredManr,6)) then begin
      RecordCheckError(200105,"",-1,"MaxMonthlyPayment");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
  end;

  if (CredManr.OrderClass!=CredMan2r.OrderClass) then begin
    CredManr.StateChangeDate = CurrentDate;
    CredManr.StateChangeTime = CurrentTime;
    CredManr.StateNotified = 0;
  end;

  if (blank(CredManr.CreatedDate)) then begin
    CredManr.CreatedDate = CurrentDate;
  end;

  if (CredManr.OKFlag!=0 and CredMan2r.OKFlag==0) then begin
    if (blank(CredManr.PayDeal)) then begin
      RecordCheckError(1058,"",-1,"PayDeal");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
  end;

  if (CredManr.SalesMan!=CredMan2r.SalesMan and CredMan2r.SalesMan!=CurrentUser) then begin
    if (UserCanAction("CredManChangeManager",false)==true) then begin
      RecordCheckError(28615,"",-1,"SalesMan");
      res = -1;
      goto LCredManVcRecordCheck;
    end;
  end;

  if (nonblank(CredManr.OfficialSerNr)) then begin
    if (GetCredLegalNrLine("CredManVc",CredManr.SerNr,CredManr.startDate,CredManr.Type,CredManr.Classifications,LegalInvNrrw,-1)) then begin
      if (LegalInvNrrw.Serie!=left(CredManr.OfficialSerNr,len(LegalInvNrrw.Serie))) then begin
        RecordCheckError(200262,"",-1,"OfficialSerNr");
        res = -1;
        goto LCredManVcRecordCheck;
      end;
    end;
  end;

  if (CredManr.MaxMonthlyPayment!=CredMan2r.MaxMonthlyPayment and CredManr.MaxMonthlyPayment>0) then begin
    BlockLoad(CredManSetb);
    BuildCredManPayPlanCust(CredManr,CredManPayPlanr,CredManSetb,true,true);
    rwcnt = MatRowCnt(CredManPayPlanr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(CredManPayPlanr,i,CredManPayPlanrw);
      if (CredManPayPlanrw.PlanType==2 and CredManPayPlanrw.Sum>CredManr.MaxMonthlyPayment) then begin
        RecordCheckError(200268,"",-1,"MaxMonthlyPayment");
        res = -1;
        goto LCredManVcRecordCheck;
      end;
    end;
  end;

  res = inner.CredManVcRecordCheck(CredManr,CredMan2r,stat,long4);
  
LCredManVcRecordCheck:;
  CredManVcRecordCheck = res;
  return;
END;


global
updating function LongInt CredManVcRecordSave(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  row CredLegalInvNrBlock LegalInvNrrw;

  res = 0;

  if (CredManr.OKFlag==1) then begin
    if (GetCredLegalNrLine("CredManVc",CredManr.SerNr,CredManr.startDate,CredManr.Type,CredManr.Classifications,LegalInvNrrw,2)) then begin
      CredManr.OfficialSerNr = NextLegalSerNr("CredManVc",CredManr.SerNr,CredManr.startDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
    end;
  end;
  if (blank(CredManr.OfficialSerNr)) then begin
    if (GetCredLegalNrLine("CredManVc",CredManr.SerNr,CredManr.startDate,CredManr.Type,CredManr.Classifications,LegalInvNrrw,1)) then begin
      CredManr.OfficialSerNr = NextLegalSerNr("CredManVc",CredManr.SerNr,CredManr.startDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
    end;
  end;

  res = inner.CredManVcRecordSave(CredManr,CredMan2r,stat,long4);
  
  CredManVcRecordSave = res;
  return;
END;

global
updating function LongInt CredManVcRecordSaveAfter(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  row CredLegalInvNrBlock LegalInvNrrw;

  res = 0;

  if (nonblank(CredManr.OfficialSerNr)) then begin
    StoreCredOffSerNrIV(CredManr.SerNr,"CredManVc",CredManr.OfficialSerNr);
  end;

  res = inner.CredManVcRecordSaveAfter(CredManr,CredMan2r,stat,long4);
  
  CredManVcRecordSaveAfter = res;
  return;
END;


global
updating function LongInt CredManVcRecordUpdate(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  row CredLegalInvNrBlock LegalInvNrrw;

  res = 0;

  if (CredManr.OKFlag==1 and CredMan2r.OKFlag!=1 and blank(CredManr.OfficialSerNr)) then begin
    if (GetCredLegalNrLine("CredManVc",CredManr.SerNr,CredManr.startDate,CredManr.Type,CredManr.Classifications,LegalInvNrrw,2)) then begin
      CredManr.OfficialSerNr = NextLegalSerNr("CredManVc",CredManr.SerNr,CredManr.startDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
    end;
  end;

  res = inner.CredManVcRecordUpdate(CredManr,CredMan2r,stat,long4);
  
  CredManVcRecordUpdate = res;
  return;
END;


updating procedure CheckAndCloseCredManTasks(record CredManVc CredManr)
begin
  record CredManCloseTaskBlock CTb;
  record ActVc Actr,oldActr;
  record RLinkVc RLr;
  Integer i;
  
  BlockLoad(CTb);
  if (nonblank(CTb.CredManOrderClass)) then begin
    if (nonblank(CredManr.OrderClass) and SetInSet(CredManr.OrderClass,CTb.CredManOrderClass)) then begin
      i = 1;
      while (ReadRecordLink(CredManr,i,Actr,RLr)) begin
        if (Actr.OKFlag==0) then begin
          RecordCopy(oldActr,Actr);
          Actr.OKFlag = 1;
          RecordUpdate(oldActr,Actr,true);
        end;
        i = i + 1;
      end;
    end;
  end;

  return;
end;

global
updating function LongInt CredManVcRecordUpdateAfter(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
begin
  Longint res;

  res = 0;

  if (CredManr.OrderClass!=CredMan2r.OrderClass) then begin
    CheckAndCloseCredManTasks(CredManr);
  end;
  if (CredManr.OfficialSerNr!=CredMan2r.OfficialSerNr) then begin
    StoreCredOffSerNrIV(CredManr.SerNr,"CredManVc",CredManr.OfficialSerNr)
  end;

  res = inner.CredManVcRecordUpdateAfter(CredManr,CredMan2r,stat,long4);

  CredManVcRecordUpdateAfter = res;
  return;
end;

global
function val GetCustomerOpenInvoices(string custcode,Longint credmannr,var val openval)
begin
  record ARVc ARr;
  val res;
  Boolean TrHs;
  record IVVc IVr;

  openval = blankval;
  TrHs = true;
  ARr.CustCode = custcode;
  while (LoopKey("CustCode",ARr,1,TrHs)) begin
    if (ARr.CustCode!=custcode) then begin
      TrHs = false;
    end else begin
      openval = openval + ARr.RVal;
      IVr.SerNr = ARr.InvoiceNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        if (IVr.CustCredManNr==credmannr) then begin
          res = res + ARr.RVal;
        end;
      end;
    end;
  end;

  GetCustomerOpenInvoices = res;
  return;
end;

global
function val GetSellerOpenInvoices(string custcode,Longint credmannr,var val openval)
begin
  record ARVc ARr;
  val res;
  Boolean TrHs;
  record IVVc IVr;
  record CredManGrntReportVc GRr;

  openval = blankval;
  TrHs = true;
  GRr.CustCode = custcode;
  GRr.CustPaidFlag = 0;
  while (LoopKey("CustCode:" & credmannr,GRr,2,TrHs)) begin
    if (GRr.CustCode!=custcode or GRr.CustPaidFlag!=0) then begin
      TrHs = false;
    end else begin
      ARr.InvoiceNr = GRr.BuyerIVNr;
      if (ReadFirstMain(ARr,1,true)) then begin
        openval = openval + ARr.RVal;
        res = res + ARr.RVal;
      end;
    end;
  end;

  GetSellerOpenInvoices = res;
  return;
end;

global
function LongInt CredManFactVcRecordCheck(var record CredManFactVc CMFr,record CredManFactVc CMF2r,LongInt stat,LongInt long4)
begin
  Longint res;
  record CredManVc CredManr;
  row CredManFactVc CMFrw;
  Integer i,rwcnt;
  val openval,usedval;

  res = 0;
  
  CredManr.SerNr = CMFr.CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    if (CMFr.Total>CredManr.InvSum4) then begin
      RecordCheckError(200101,"",-1,"Total");
      res = -1;
      goto LCredManFactVcRecordCheck;
    end;
  end;
  rwcnt = MatRowCnt(CMFr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(CMFr,i,CMFrw);
    usedval = GetCustomerOpenInvoices(CMFrw.CustCode,CredManr.SerNr,openval);
    if (usedval>CMFrw.MaxLimit) then begin
      RecordCheckError(200104,openval,i,"MaxLimit");
      res = -1;
      goto LCredManFactVcRecordCheck;
    end;
  end;

LCredManFactVcRecordCheck:;
  CredManFactVcRecordCheck = res;
  return;
end;

global
function LongInt CredManPmtGrntVcRecordCheck(var record CredManPmtGrntVc PGr,record CredManPmtGrntVc PG2r,LongInt stat,LongInt long4)
begin
  Longint res;
  record CredManVc CredManr;
  row CredManPmtGrntVc PGrw;
  Integer i,rwcnt;
  val openval,usedval;

  res = 0;
  
  CredManr.SerNr = PGr.CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    if (PGr.Total>CredManr.InvSum4) then begin
      RecordCheckError(200101,"",-1,"Total");
      res = -1;
      goto LCredManPmtGrntVcRecordCheck;
    end;
  end;
  rwcnt = MatRowCnt(PGr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PGr,i,PGrw);
    usedval = GetSellerOpenInvoices(PGrw.CustCode,CredManr.SerNr,openval);
    if (usedval>PGrw.MaxLimit) then begin
      RecordCheckError(200104,openval,i,"MaxLimit");
      res = -1;
      goto LCredManPmtGrntVcRecordCheck;
    end;
  end;

LCredManPmtGrntVcRecordCheck:;
  CredManPmtGrntVcRecordCheck = res;
  return;
end;

global
function Boolean ContractHasHistory(record CredManVc CredManr)
begin
  record CredHistVc CredHistr;
  Boolean res;
  
  CredHistr.CredManNr = CredManr.SerNr;
  if (ReadFirstKey("CredManChargeType",CredHistr,1,true)) then begin
    res = true;
  end;

  ContractHasHistory = res;
  return;
end;

global
function LongInt CredManVcRecordRemoveTest(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record CredManEventVc CredManEventr;
  string 200 thekey;

  res = inner.CredManVcRecordRemoveTest(CredManr,CredMan2r,stat,long4);

  if (res!=0) then begin
    if (ContractHasHistory(CredManr)) then begin
      res = 0;
    end;
  end;

  if (res!=0) then begin
    if (UserCanAction("CredManNoDelete",false)==true) then begin
      res = 0;
    end;
  end;

  if (res!=0) then begin
    if (CredManr.Approved!=0) then begin
      res = 0;
    end;
  end;
  
  CredManVcRecordRemoveTest = res;
  return;
END;

updating
procedure RemoveCredManSchedule(Longint sernr)
begin
  record CredManSchedVc CSr;

  CSr.CredManNr = sernr;
  if (ReadFirstMain(CSr,1,true)) then begin
    RecordRemove(CSr);
  end;

  return;
end;

global
updating function LongInt CredManVcRecordRemoveAfter(var record CredManVc CredManr,record CredManVc CredMan2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  res = 0;
  
  ClearAgreementDocRecords(CredManr.SerNr,0);
  RemoveCredManSchedule(CredManr.SerNr);
  if (nonblank(CredManr.OfficialSerNr)) then begin
    DeleteCredOffSerNr(CredManr.SerNr,"CredManVc");
  end;
  
  CredManVcRecordRemoveAfter = res;
  return;
end;

global
function LongInt CredManMoneyRecVcRecordCheck(var record CredManMoneyRecVc MRr,record CredManMoneyRecVc MR2r,LongInt stat,LongInt long4)
begin
  Longint res;
  record CredManVc CredManr;

  res = 0;
  
  CredManr.SerNr = MRr.CredManNr;
  if (ReadFirstMain(CredManr,1,true)) then begin
    if (MRr.Total!=CredManr.InvSum4) then begin
      RecordCheckError(200269,"",-1,"Total");
      res = -1;
      goto LCredManMoneyRecVcRecordCheck;
    end;
  end;

LCredManMoneyRecVcRecordCheck:;
  CredManMoneyRecVcRecordCheck = res;
  return;
end;


global
function LongInt CredManAccVcRecordCheck(var record CredManAccVc CMAr,record CredManAccVc CMA2r,LongInt stat,LongInt long4)
begin
  Longint res;
  row CredManAccVc CMArw;
  Integer i,rwcnt;
  string 255 tstr;

  res = 0;
  
  rwcnt = MatRowCnt(CMAr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(CMAr,i,CMArw);
    if (GetAccName(CMArw.DebitAcc,tstr,60)==false) then begin
      RecordCheckError(1120,CMArw.DebitAcc,i,"DebitAcc");
      res = -1;
      goto LCredManAccVcRecordCheck;
    end;
    if (GetAccName(CMArw.CreditAcc,tstr,60)==false) then begin
      RecordCheckError(1120,CMArw.CreditAcc,i,"CreditAcc");
      res = -1;
      goto LCredManAccVcRecordCheck;
    end;
  end;
  if (GetAccName(CMAr.PLCreditAcc,tstr,60)==false) then begin
    RecordCheckError(1120,CMAr.PLCreditAcc,-1,"PLCreditAcc");
    res = -1;
    goto LCredManAccVcRecordCheck;
  end;
  if (GetAccName(CMAr.PLDebitAcc,tstr,60)==false) then begin
    RecordCheckError(1120,CMAr.PLDebitAcc,-1,"PLDebitAcc");
    res = -1;
    goto LCredManAccVcRecordCheck;
  end;
  if (GetAccName(CMAr.ControlAcc,tstr,60)==false) then begin
    RecordCheckError(1120,CMAr.ControlAcc,-1,"ControlAcc");
    res = -1;
    goto LCredManAccVcRecordCheck;
  end;
  if (GetAccName(CMAr.DebtorsAccShort,tstr,60)==false) then begin
    RecordCheckError(1120,CMAr.DebtorsAccShort,-1,"DebtorsAccShort");
    res = -1;
    goto LCredManAccVcRecordCheck;
  end;
  if (GetAccName(CMAr.DebtorsAccLong,tstr,60)==false) then begin
    RecordCheckError(1120,CMAr.DebtorsAccLong,-1,"DebtorsAccLong");
    res = -1;
    goto LCredManAccVcRecordCheck;
  end;

LCredManAccVcRecordCheck:;
  CredManAccVcRecordCheck = res;
  return;
end;
