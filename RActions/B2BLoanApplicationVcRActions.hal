//<halrule>server-only</halrule>
external function Boolean InsertManagerInB2BApplication(var record B2BLoanApplicationVc);
external updating procedure ClearAgreementDocRecords(Longint,Integer);
external inner function string 50 NextLegalSerNr(string,LongInt,Date,string,string,string);
external updating procedure DeleteCredOffSerNr(LongInt,string);
external updating procedure StoreCredOffSerNrIV(LongInt,string,string);
external function Boolean GetCredLegalNrLine(string,string,Date,string,string,var row CredLegalInvNrBlock,Integer);
external procedure CredManAutoToolsAssignB2BLoanManager(var record B2BLoanApplicationVc); 

global
function LongInt B2BLoanApplicationVcRecordDefaults(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  record CredManSetBlock CredManSb;
  record CMOrderClassBlock OCb;
  
  res = 1;  

  LAr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    LAr.SerNr = NextSerNr("B2BLoanApplicationVc",LAr.TransDate,-1,false,"");
  end;
  LAr.Manager = CurrentUser;
  InsertManagerInB2BApplication(LAr);
 
  BlockLoad(CredManSb);
  LAr.MonthlyPaymentDay = CredManSb.InvDay;

  BlockLoad(OCb);
  LAr.OrderClass = OCb.DefOrderClass;
  
  B2BLoanApplicationVcRecordDefaults = res; 
  return;
end;

global
function LongInt B2BLoanApplicationVcRecordDuplicate(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt long3,LongInt long4)
begin
  LongInt res; 
  res = 1;  

  LAr.TransDate = CurrentDate;
  LAr.SerNr = -1;
  LAr.Manager = CurrentUser;
  LAr.StateChangeDate = CurrentDate;
  LAr.StateChangeTime = CurrentTime;
  LAr.StateNotified = 0;
  LAr.OfficialSerNr = "";

  InsertManagerInB2BApplication(LAr);


  B2BLoanApplicationVcRecordDuplicate = res; 
  return;
end;

global
updating function LongInt B2BLoanApplicationVcRecordCheck(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record CUVc CUr,oldCUr;

  res = 0;
  if (LAr.SerNr<=0) then begin
    LAr.SerNr = NextSerNr("B2BLoanApplicationVc",LAr.TransDate,-1,false,"");
  end;
  if (nonblank(LAr.BankCode) or nonblank(LAr.BankAccount)) then begin
    CUr.Code = LAr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      if (blank(CUr.AccOperator) or blank(CUr.BankAccount)) then begin
        CUr.Code = LAr.CustCode;
        if (ReadFirstMain(CUr,1,true)) then begin
          RecordCopy(oldCUr,CUr);
          CUr.BankAccount = LAr.BankAccount;
          CUr.AccOperator = LAr.BankCode;
          RecordUpdate(oldCUr,CUr,false);
        end;
      end;
    end;
  end;

  if (LAr.OrderClass!=LA2r.OrderClass) then begin
    LAr.StateChangeDate = CurrentDate;
    LAr.StateChangeTime = CurrentTime;
    LAr.StateNotified = 0;
  end;

  if (blank(LAr.CreatedDate)) then begin
    LAr.CreatedDate = CurrentDate;
  end;

  if (LAr.Manager!=LA2r.Manager and LA2r.Manager!=CurrentUser) then begin
    if (UserCanAction("LoanAppChangeManager",false)==true) then begin
      RecordCheckError(28615,"",-1,"Manager");
      res = -1;
      goto LB2BLoanApplicationVcRecordCheck;
    end;
  end;

LB2BLoanApplicationVcRecordCheck:;

  B2BLoanApplicationVcRecordCheck = res;
  return;
end;

updating procedure CheckAndCloseLoanAppTasks(record B2BLoanApplicationVc LAr)
begin
  record CredManCloseTaskBlock CTb;
  record ActVc Actr,oldActr;
  record RLinkVc RLr;
  Integer i;
  
  BlockLoad(CTb);
  if (nonblank(CTb.LoanAppOrderClass)) then begin
    if (nonblank(LAr.OrderClass) and SetInSet(LAr.OrderClass,CTb.LoanAppOrderClass)) then begin
      i = 1;
      while (ReadRecordLink(LAr,i,Actr,RLr)) begin
        if (Actr.OKFlag==0) then begin
          RecordCopy(oldActr,Actr);
          Actr.OKFlag = 1;
          RecordUpdate(oldActr,Actr,true);
        end;
        i = i + 1;
      end;
    end;
  end;

  return;
end;

global
updating function LongInt B2BLoanApplicationVcRecordSave(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  Longint res;
  row CredLegalInvNrBlock LegalInvNrrw;

  res = 0;

  if (blank(LAr.OfficialSerNr)) then begin
    if (GetCredLegalNrLine("B2BLoanApplicationVc",LAr.SerNr,LAr.TransDate,LAr.Type,"",LegalInvNrrw,1)) then begin
      LAr.OfficialSerNr = NextLegalSerNr("B2BLoanApplicationVc",LAr.SerNr,LAr.TransDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
    end;
  end;
  CredManAutoToolsAssignB2BLoanManager(LAr);
  
  B2BLoanApplicationVcRecordSave = res;
  return;
end;

global
updating function LongInt B2BLoanApplicationVcRecordUpdate(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  Longint res;
  row CredLegalInvNrBlock LegalInvNrrw;

  res = 0;

  if (blank(LAr.OfficialSerNr)) then begin
    if (GetCredLegalNrLine("B2BLoanApplicationVc",LAr.SerNr,LAr.TransDate,LAr.Type,"",LegalInvNrrw,1)) then begin
      LAr.OfficialSerNr = NextLegalSerNr("B2BLoanApplicationVc",LAr.SerNr,LAr.TransDate,LegalInvNrrw.Serie,LegalInvNrrw.TSerStart,LegalInvNrrw.TSerEnd);
    end;
  end;
  if (LAr.OrderClass!=LA2r.OrderClass) then begin
    CredManAutoToolsAssignB2BLoanManager(LAr);
  end;
  
  B2BLoanApplicationVcRecordUpdate = res;
  return;
end;


global
updating function LongInt B2BLoanApplicationVcRecordUpdateAfter(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  Longint res;

  res = 0;

  if (LAr.OrderClass!=LA2r.OrderClass) then begin
    CheckAndCloseLoanAppTasks(LAr);
  end;

  B2BLoanApplicationVcRecordUpdateAfter = res;
  return;
end;


global
updating function LongInt B2BLoanApplicationVcRecordRemoveAfter(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  res = 0;
  
  ClearAgreementDocRecords(LAr.SerNr,1);
  if (nonblank(LAr.OfficialSerNr)) then begin
    DeleteCredOffSerNr(LAr.SerNr,"B2BLoanApplicationVc");
  end;
  
  B2BLoanApplicationVcRecordRemoveAfter = res;
  return;
end;

global
updating function LongInt B2BLoanApplicationVcRecordRemoveTest(var record B2BLoanApplicationVc LAr,record B2BLoanApplicationVc LA2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  res = 1;
  
  if (UserCanAction("LoanAppNoDelete",false)==true) then begin
    res = 0;
  end;
  
  B2BLoanApplicationVcRecordRemoveTest = res;
  return;
end;